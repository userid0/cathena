prontera.gat,152,190,5	script	Real Estate Agent	109,{

//Number of houses in each town//
//Prontera #1-5//
set @housetotal[1],3;

//Morocc #6-10//
set @housetotal[2],0;

//Payon #11-15//
set @housetotal[3],0;

//Juno #16-20//
set @housetotal[4],0;

//Geffen #21-25//
set @housetotal[5],0;

//Gonryun #26-30//
set @housetotal[6],0;

//Umbala #31-35//
set @housetotal[7],0;

//Izlude #36-40//
set @housetotal[8],0;

//Lutie #41-45//
set @housetotal[9],0;

//Al De Baran #46-50//
set @housetotal[10],0;

//Amatsu #51-55//
set @housetotal[11],0;

//Comodo #56-60//
set @housetotal[12],0;

//Alberta #61-65//
set @housetotal[13],0;

set @avail_count,1;
set @status_count, 1;
set @house_avail[1],0;
L_AVAIL_LOOP:
	if ( @housetotal[@avail_count] == 0 ) goto L_NEXT_AVAIL;
	set @house_avail[@avail_count], @house_avail[@avail_count] + 1 - $housestatus[( @status_count + ( ( @avail_count - 1 ) * 5 ) )];
	set @status_count, @status_count + 1;
	if ( @status_count <= @housetotal[@avail_count] ) goto L_AVAIL_LOOP;
L_NEXT_AVAIL:
	set @avail_count, @avail_count + 1;
	set @status_count, 1;
	set @house_avail[@avail_count],0;
	if ( @avail_count <= 13 ) goto L_AVAIL_LOOP;

//If character owns a house, goto the homeowner menu//
if ( $houseowner$[warpnum] == strcharinfo(0) ) goto L_OWNER_MENU;


mes "[ ^0000FFReal Estate Agent ^000000]";
mes "Hey there!  I've got a great offer for you today!";
next;
mes "[ ^0000FFReal Estate Agent ^000000]";
mes "How would you like to own your very own house?";
next;
menu "Umm.... OK",L_HOUSE,"Warp Me To Someone Elses House",L_HOUSE2,"No Thanks",L_QUIT;

L_HOUSE:
	set @switch, 0;
	set @count,0;
	set @showcount, 1;
	mes "[ ^0000FFReal Estate Agent ^000000]";
	mes "Alright then, let's quit wasting time";
	mes "Where do you want to buy a house?";
	next;
	menu "Prontera [" + @house_avail[1] + " / " + @housetotal[1] + "]",L_PRONT,
//	"Morocc [" + @house_avail[2] + " / " + @housetotal[2] + "]",L_MOC,
//	"Payon [" + @house_avail[3] + " / " + @housetotal[3] + "]",L_PAY,
//	"Geffen [" + @house_avail[5] + " / " + @housetotal[5] + "]",L_GEF,
//	"Gonryun [" + @house_avail[6] + " / " + @housetotal[6] + "]",L_GON,
//	"Juno [" + @house_avail[4] + " / " + @housetotal[4] + "]",L_YUNO,
//	"Umbala [" + @house_avail[7] + " / " + @housetotal[7] + "]",L_UMB,
//	"Izlude [" + @house_avail[8] + " / " + @housetotal[8] + "]",L_IZL,
//	"Lutie [" + @house_avail[9] + " / " + @housetotal[9] + "]",L_LUT,
//	"Al De Baran [" + @house_avail[10] + " / " + @housetotal[10] + "]",L_ALDE,
//	"Amatsu [" + @house_avail[11] + " / " + @housetotal[11] + "]",L_AMAT,
//	"Comodo [" + @house_avail[12] + " / " + @housetotal[12] + "]",L_COM,
	"Alberta [" + @house_avail[13] + " / " + @housetotal[13] + "]",L_ALB;

L_HOUSE2:
	set @switch,1;
	set @count,0;
	set @showcount,1;
	mes "[ ^0000FFReal Estate Agent ^000000]";
	mes "What town is the house in?";
	next;
	menu "Prontera [" + ( @housetotal[1] - @house_avail[1] ) + " / " + @housetotal[1] + "]",L_PRONT,
//	"Morocc [" + ( @housetotal[2] - @house_avail[2] ) + " / " + @housetotal[2] + "]",L_MOC,
//	"Payon [" + ( @housetotal[3] - @house_avail[3] ) + " / " + @housetotal[3] + "]",L_PAY,
//	"Geffen [" + ( @housetotal[5] - @house_avail[5] ) + " / " + @housetotal[5] + "]",L_GEF,
//	"Gonryun [" + ( @housetotal[6] - @house_avail[6] ) + " / " + @housetotal[6] + "]",L_GON,
//	"Juno [" + ( @housetotal[4] - @house_avail[4] ) + " / " + @housetotal[4] + "]",L_YUNO,
//	"Umbala [" + ( @housetotal[7] - @house_avail[7] ) + " / " + @housetotal[7] + "]",L_UMB,
//	"Izlude [" + ( @housetotal[8] - @house_avail[8] ) + " / " + @housetotal[8] + "]",L_IZL,
//	"Lutie [" + ( @housetotal[9] - @house_avail[9] ) + " / " + @housetotal[9] + "]",L_LUT,
//	"Al De Baran [" + ( @housetotal[10] - @house_avail[10] ) + " / " + @housetotal[10] + "]",L_ALDE,
//	"Amatsu [" + ( @housetotal[11] - @house_avail[11] ) + " / " + @housetotal[11] + "]",L_AMAT,
//	"Comodo [" + ( @housetotal[12] - @house_avail[12] ) + " / " + @housetotal[12] + "]",L_COM,
	"Alberta [" + ( @housetotal[13] - @house_avail[13] ) + " / " + @housetotal[13] + "]",L_ALB;

	L_PRONT:
		set @base,1;
		goto L_VIEW_HOUSES;
	L_MOC:
		set @base,2;
		goto L_VIEW_HOUSES;
	L_PAY:
		set @base,3;
		goto L_VIEW_HOUSES;
	L_YUNO:
		set @base,4;
		goto L_VIEW_HOUSES;
	L_GEF:
		set @base,5;
		goto L_VIEW_HOUSES;
	L_GON:
		set @base,6;
		goto L_VIEW_HOUSES;
	L_UMB:
		set @base,7;
		goto L_VIEW_HOUSES;
	L_IZL:
		set @base,8;
		goto L_VIEW_HOUSES;
	L_LUT:
		set @base,9;
		goto L_VIEW_HOUSES;
	L_ALDE:
		set @base,10;
		goto L_VIEW_HOUSES;
	L_AMAT:
		set @base,11;
		goto L_VIEW_HOUSES;
	L_COM:
		set @base,12;
		goto L_VIEW_HOUSES;
	L_ALB:
		set @base,13;
		goto L_VIEW_HOUSES;

	L_VIEW_HOUSES:
		set @count, @count + 1;
		set @housenum, @count + ( ( @base - 1 ) * 5 );
		if ( ( @house_avail[@base] == 0 ) && ( @switch == 0) ) goto L_NOAVAIL;
		if ( ( @house_avail[@base] == @housetotal[@base] ) && ( @switch == 1 ) ) goto L_NONE_OWNED;
		if ( @showcount > @house_avail[@base] && (@switch == 0 ) ) goto L_NOMORE;
		if ( @showcount > ( @housetotal[@base] - @house_avail[@base] )  && ( @switch == 1) ) goto L_NOMORE_OWNED;
		if ( $housestatus[@housenum]  == @switch ) goto L_VIEW_SUB1;
		goto L_VIEW_HOUSES;
		L_VIEW_SUB1:
			mes "[ ^0000FFReal Estate Agent ^000000]";
			if ( @switch == 1 ) goto L_VIEW_SUB2;
			mes "[ House No." + @housenum + " ]";
			mes "  [ Status:  For Sale ]";
			mes "  [ Cost:  " + $houseprice[@housenum] + "z ]";
			mes "  [ " + @showcount + " / " + @house_avail[@base] + " Available Houses ]";
			set @showcount, @showcount +1;
			next;
			menu "Buy This House",L_BUY,"Take a tour",L_WARP,"Look at another house",L_VIEW_HOUSES,"Quit",L_QUIT;
		L_VIEW_SUB2:
			mes "[ Title: " + $housename$[@housenum] + " ]";
			mes "  [ Owner:  " + $houseowner$[@housenum] + " ]";
			set @showcount, @showcount +1;
			next;
			if ( $houselock[@housenum] == 1 ) goto L_VIEW_SUB2_2;
			menu "Warp here",L_WARP,"No, Not This House",L_VIEW_HOUSES,"Nevermind",L_QUIT;
		L_VIEW_SUB2_2:
			menu "Warp here (Password Required)",L_GET_PASS,"No",L_VIEW_HOUSES,"Nevermind",L_QUIT;

	L_BUY:
		if ( Zeny < $houseprice[@housenum] ) goto L_NEEDZENY;
		set Zeny, Zeny - $houseprice[@housenum];
		set $housestatus[@housenum], 1;
		set $houseowner$[@housenum], strcharinfo(0);
		set warpnum, @housenum;
		set $housename$[@npcnum],"House No " + @housenum;
		set @npcname$, "butler" + @housenum;
		enablenpc @npcname$;
		goto L_CONGRATS;

	L_GET_PASS:
		mes "[ ^0000FFReal Estate Agent ^000000]";
		mes "That door is locked, if you give me the correct password, I can let you in.  ^FF0000Reminder: Passwords are case sensitive!^000000";
		next;
		input @passguess$;
		if ( @passguess$ == $housepass$[@housenum] ) goto L_WARP;
		mes "[ ^0000FFReal Estate Agent ^000000]";
		mes "Sorry that's the wrong password";
		next;
		menu "Oops! Let me try again!",L_GET_PASS,"Sorry, Wrong House!",L_HOUSE2;

	L_OWNER_MENU:
		set @housenum, @housenum + 1;
		mes "[ ^0000FFReal Estate Agent ^000000]";
		mes "Shall I warp you to your house?";
		next;
		menu "Yes, Right Away!",L_WARP_OWNER,"Nah, Changed My Mind",L_QUIT,"Warp Me To Someone Elses House",L_HOUSE2;
	L_WARP_OWNER:
		set @housenum, warpnum;
		goto L_WARP;


	L_NOMORE:
		mes "[ ^0000FFReal Estate Agent ^000000]";
		mes "I'm sorry, we don't have any more properties available in that area.  Would you like to take a look at another town?";
		next;
		menu "OK",L_HOUSE,"No Thanks.",L_QUIT;
	L_NOAVAIL:
		mes "[ ^0000FFReal Estate Agent ^000000]";
		mes "I'm sorry, we don't have any property available in that town right now, would you like to look at another area?";
		next;
		menu "Sure",L_HOUSE,"No Thanks.",L_QUIT;
	L_NONE_OWNED:
		mes "[ ^0000FFReal Estate Agent ^000000]";
		mes "Nobody owns a house in that area! Would you like to look at another town?";
		next;
		menu "Yep",L_HOUSE2,"Nope, See Ya Later",L_QUIT;
	L_NOMORE_OWNED:
		mes "[ ^0000FFReal Estate Agent ^000000]";
		mes "There are no more houses owned in that town.  Look at another place?";
		next;
		menu "Of Course!",L_HOUSE2,"Forget it",L_QUIT;
	L_NEEDZENY:
		mes "[ ^0000FFReal Estate Agent ^000000]";
		mes "You don't have enough zeny to buy that house.  Would you like to look at something else?";
		next;
		menu "Yes",L_HOUSE,"No",L_QUIT;
	L_QUIT:
		mes "[ ^0000FFReal Estate Agent ^000000]";
		mes "Sorry I couldn't help you... come back if you need anything.";
		close;
	L_CONGRATS:
		mes "[ ^0000FFReal Estate Agent ^000000]";
		mes "Congratulations, you're a homeowner now, the deed has been signed over to your name!";
		announce strcharinfo(0) + " is now the proud owner of a new home.",0;
		next;
		goto L_OWNER_MENU;

	L_WARP:
		warp $map$[@housenum],$svx[@housenum],$svy[@housenum];
		close;

}