//===== eAthena Script =======================================
//= War of Emperium Guild Flags Function
//===== By: ==================================================
//= jAthena - kalen (1.0)
//= 1.1 by Akaru, ho|yAnge|X, and Valaris
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= eAthena 0.1+; RO Episode 4+
//===== Description: =========================================
//= Flags display messages stating whether or not a guild castle has been captured.
//= Allows the guild members to return to a captured castle.
//======================================
//= Break down of arguments used in the function:
//=   arg(0): name of City.
//=   arg(1): name of guild castle.
//=   arg(2): x1 coordinate for warp
//=   arg(3): y1 coordinate for warp
//===== Additional Comments: =================================
//= v1.2: All Guild flags scripts use this function. [kobra_k88]
//============================================================

function	script	F_Flags	{

	set @GID, GetCastleData(getarg(1)+".gat",1);
	if (@GID != 0) goto L_Startg;

	mes "[ Edict of the Divine Rune Midgard Kingdom of " + getarg(0) + " ]";
	mes " ";
	mes "1. Honoring the ordinance of the Divine Rune Midgard Kingdom of " + getarg(0) + ", this guild base has not been taken by any guild yet";
	mes " ";
	mes "2. In order to take this guild base, you must defeat all the guardians that are protecting the guild base and eliminate the Emperium.";
	return;
L_Startg:
	if (getcharid(2) == @GID) goto L_Startg2;
	mes "[ Edict of the Divine Rune Midgard Kingdom of "+ getarg(0) +" ]";
	mes " ";
	mes "1. Honoring the ordinance of the";
	mes "Divine Rune Midgard Kingdom of ";
	mes getarg(0) +", we approve that this";
	mes "base is in the private possession";
	mes "of the ^ff0000" + GetGuildName(@GID) + "^000000 guild.";
	mes " ";
	mes "2. The guild master of the";
	mes "^ff0000"+ GetGuildName(@GID) + "^000000 guild, is ^0000FF" + GetGuildMaster(@GID) + "^000000.";
	mes "If anyone objects to that, raise your sword to honor yourself.";
	return;
L_Startg2:
	mes "[ Ringing Voice ]";
	mes "Brave one... would you return to your honorable be?";
	next;
	menu "Return.",M_Enter,"Cancel.",-;
	return;

	M_Enter:
		warp getarg(1)+".gat",getarg(2),getarg(3);
		return;
}
