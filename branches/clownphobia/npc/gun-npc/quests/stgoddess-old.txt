prontera.gat,164,169,3	script	Status Goddess	811,{
	mes "[Status Goddess]";
	mes "Hello there mortal!";
	next;
	mes "[Status Goddess]";
	mes "I am the goddess of stats, I can modify your stats and skills.";
	mes "I can reset your skills, stats,";
	mes "and raise your stats beyond the normal mortal limits.";
	next;
	mes "[Status Goddess]";
	mes "Please select the service you wish:";
	next;
	menu "Reset Stats or Skills",MenReset,"Raise Stats",MenStat;
	MenReset:
	menu "Reset Skills - 8000z",L0,"Reset Stats - 8000z",L1,"Reset Both Stats and Skills - 14400z",L2,"Quit",LEnd;
	
	L0:
		mes "[Status Goddess]";
		if (Zeny < 8000) goto NeedZenys;
		mes "Alright, here we go now..";
		mes "Enjoy your newfound skills.";
		set Zeny,zeny-8000;
		ResetSkill;
		close;
	L1:
		mes "[Status Goddess]";
		if (Zeny < 8000) goto NeedZenys;
		mes "Alright, here we go now..";
		mes "Enjoy your newfound strength.";
		set Zeny,zeny-8000;
		ResetStatus;
		close;
	L2:
		mes "[Status Goddess]";
		if (Zeny < 14400) goto NeedZenys;
		mes "Alright, here we go now..";
		mes "Enjoy your newfound skills.";
		set Zeny,zeny-14400;
		ResetSkill;
		ResetStatus;
		close;

		NeedZenys:
			mes "[Status Goddess]";
			mes "Sorry, you don't have enough Zeny.";
			close;
	LEnd:
		mes "[Status Goddess]";
		mes "Perhaps some other time then?";
		close;	
		
	MenStat:
if (sg_use == 0) set sg_use,1;
set @currprice,(sg_use*125000);
set @notava$,"^C00000Not Available - ";
if (@strava$ == @notava$) set @strava$,"";
if (readparam(13)<99) set @strava$,@notava$;
if (@agiava$ == @notava$) set @agiava$,"";
if (readparam(14)<99) set @agiava$,@notava$;
if (@vitava$ == @notava$) set @vitava$,"";
if (readparam(15)<99) set @vitava$,@notava$;
if (@intava$ == @notava$) set @intava$,"";
if (readparam(16)<99) set @intava$,@notava$;
if (@dexava$ == @notava$) set @dexava$,"";
if (readparam(17)<99) set @dexava$,@notava$;
if (@lukava$ == @notava$) set @lukava$,"";
if (readparam(18)<99) set @lukava$,@notava$;

	mes "[Status Goddess]";
	mes "I will provide my services, but it will cost you quite a fee. Every";
	mes "time you increase your stats the price will increase by ^0000FF125,000z^000000.";
	mes "Also, I may not modify your stats until they reach thier peak, nor can I add past 125.";
	next;
	mes "[Status Goddess]";
	mes "The current cost for you is ^0000FF"+@currprice+"z^000000.";
	mes "Please select the stat you wish to increase.";
	mes "Remember, it will still take status points normally.";
	next;
	menu 
	"^0000FF"+@strava$+"Strength^000000",AddStr,
	"^0000FF"+@agiava$+"Agility^000000",AddAgi,
	"^0000FF"+@vitava$+"Vitality^000000",AddVit,
	"^0000FF"+@intava$+"Intelligence^000000",AddInt,
	"^0000FF"+@dexava$+"Dexterity^000000",AddDex,
	"^0000FF"+@lukava$+"Luck^000000",AddLuk;
	
		AddStr:
		set @stnum,13;
		if (@strava$ != "") goto NoPeak;
		goto StatCalc;
		AddAgi:
		set @stnum,14;
		if (@agiava$ != "") goto NoPeak;
		goto StatCalc;
		AddVit:
		set @stnum,15;
		if (@vitava$ != "") goto NoPeak;
		goto StatCalc;
		AddInt:
		set @stnum,16;
		if (@intava$ != "") goto NoPeak;
		goto StatCalc;
		AddDex:
		set @stnum,17;
		if (@dexava$ != "") goto NoPeak;
		goto StatCalc;
		AddLuk:
		set @stnum,18;
		if (@lukava$ != "") goto NoPeak;
		goto StatCalc;	
	
	StatCalc:
		set @statreq,0;
		set @currstat,readparam(@stnum);
		if (@currstat>=125) goto OverLimit;
		if ((@currstat>=121)&&(!@statreq)) set @statreq,14;
		if ((@currstat>=111)&&(!@statreq)) set @statreq,13;
		if ((@currstat>=101)&&(!@statreq)) set @statreq,12;
		if ((@currstat>=99)&&(!@statreq)) set @statreq,11;
		if (@stnum==13) set @stms$,"strength";
		if (@stnum==14) set @stms$,"agility";
		if (@stnum==15) set @stms$,"vitality";
		if (@stnum==16) set @stms$,"intelligence";
		if (@stnum==17) set @stms$,"dexterity";
		if (@stnum==18) set @stms$,"luck";
		
			mes "[Status Goddess]";
			mes "Are you sure you want to increase your ^0000FF"+@stms$+"^000000 to "+(@currstat+1)+"?";
			mes "This will cost you ^FF0000"+@currprice+"^000000 zeny and requires "+@statreq+" stat points.";
			next;
			menu "Yes",-,"No",LEnd;
			if (StatusPoint < @statreq) goto NoStat;
			if (Zeny < @currprice) goto NeedZenys;
			set Zeny,Zeny-@currprice;
			set sg_use,sg_use+1;
			statusup @stnum;
			mes "[Status Goddess]";
			mes "Ah....";
			next;
			mes "[Status Goddess]";
			mes "I can see you growing stronger already!";
			mes "Enjoy your greater power...";
			close;
	
	OverLimit:
	mes "[Status Goddess]";
	mes "You did not listen, it is impossible for me to raise your status beyond 125!";
	mes "It is far past even my limits...";
	close;
	
	NoPeak:
	if (@stnum==13) set @stms$,"strength";
	if (@stnum==14) set @stms$,"agility";
	if (@stnum==15) set @stms$,"vitality";
	if (@stnum==16) set @stms$,"intelligence";
	if (@stnum==17) set @stms$,"dexterity";
	if (@stnum==18) set @stms$,"luck";
	mes "[Status Goddess]";
	mes "You did not listen, I cannot do anything until your ^0000FF"+@stms$+"^000000";
	mes "is at its peak. Please return when your ^0000FF"+@stms$+"^000000 is 99 or above";
	close;
	
	NoStat:
	mes "[Status Goddess]";
	mes "I cannot raise this stat for you. You must have "+@statreq+" stat points.";
	close;
	
	
}