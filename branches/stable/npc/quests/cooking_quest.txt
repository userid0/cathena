//===== eAthena Script ======================================= 
//= Cooking Quest
//===== By: ================================================== 
//= Reddozen
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= eAthena 1.0+
//===== Description: ========================================= 
//= Official Cooking Quest
//===== Additional Comments: ================================= 
//= 1.1 Fixed wrong item ID, added missing ";, optimized [Lupus]
//= 1.1a minor bugfix, thx 2 Irmin [Lupus]
//= 1.2 Fixed exploit, some typos [Lupus]
//= 1.3 Fixed wrong label [Lupus]
//============================================================ 

prt_castle.gat,43,30,6	script	Chef Apprentice	878,{

	if(cooking == 2){
		mes "[Chef's Apprentice]";
		mes "I see you passed the test.";
		mes "Good job!";
		getitem 7472, 1;//Items: Lv1 Cookbook,
		set cook_book, 7472;
		set cooking, 3;
	}

	if(cooking == 3){
		mes "[Chef's Apprentice]";
		mes "Hi there. Is there something";
		mes "that I can help you with?";
		next;
		menu "Buy Supplies",-, "Talk about cooking",L_Cook_Talk;		

			mes "[Chef's Apprentice]";
			mes "Outdoor Cooking Set - 500z each";
			mes " How many?";
			input @amount;
			if(@amount < 1) goto ER_Invalid;
			if(@amount > 100) goto ER_TooMuch;
			if(@amount*500 > Zeny) goto ER_Zeny;
			set Zeny, Zeny-(@amount*500);
			getitem 12125, @amount;//Items: Outdoor Cooker,
			next;
			mes "[Chef's Apprentice]";
			mes "Enjoy!";
			close;

		L_Cook_Talk:
			mes "[Chef's Apprentice]";
			mes "You passed the test,";
			mes "but were your results";
			mes "worth while?";
			next;
			menu "Taste my food",-, "No, it was horrible",L_Taste_End;

			mes "[Chef's Apprentice]";
			mes "I don't know about that.";
			mes "It may not be safe...";
			next;
			menu "Please taste it",-, "You're probably right",L_Taste_End;

			mes "[Chef's Apprentice]";
			mes "Why would I want to try";
			mes "the food of a novice chef?";
			next;
			menu "Please try",-, "Yeah, I'm too new at this",L_Taste_End;

			mes "[Chef's Apprentice]";
			mes "I'll make a deal with you.";
			mes "if you cook me one of every";
			mes "Lv 1 food, then I'll try";
			mes "your Cooking...";
			next;
			menu "Agree",-, "Refuse",L_Taste_End;

			mes "[Chef's Apprentice]";
			mes "You'll need to make all";
			mes "six foods. Let me know";
			mes "when you're finished.";
			set cooking, 4;
			close;

		L_Taste_End:
			mes "[Chef's Apprentice]";
			mes "Come back when you feel";
			mes "more confident.";
			close;
	}

	if(cooking == 4){
		mes "[Chef's Apprentice]";
		if(countitem(12056) < 1 && countitem(12061) < 1 && countitem(112046) < 1 && countitem(12066) < 1 && countitem(12041) < 1 && countitem(12051) < 1){//Items: Frog Spawn Soup, Grape Juice with Honey, ???, Fried Monkey Tail, Boiled Locust, Steamed Crab Pincer,
			mes "What!? Through already?";
			mes "Let me see what you've made";
			next;

			delitem 12056, 1;//Items: Frog Spawn Soup,
			delitem 12061, 1;//Items: Grape Juice with Honey,
			delitem 12046, 1;//Items: Grape Juice and Tea,
			delitem 12066, 1;//Items: Fried Monkey Tail,
			delitem 12041, 1;//Items: Boiled Locust,
			delitem 12051, 1;//Items: Steamed Crab Pincer,

			mes "[Chef's Apprentice]";
			mes "Great job, but there's";
			mes "still one thing that I need";
			mes "you to do. Talk to a friend";
			mes "of mine in Payon. We used";
			mes "to study together.";
			next;
			mes "[Chef's Apprentice]";
			mes "Take him this Leather Pouch,";
			mes "and I'll put in a good word";
			mes "for you with my teacher.";

			getitem 7432, 1;//Items: Leather Pouch,
			set cooking, 5;
		} else {
			mes "Please come back when you've cooked";
			mes "all the Lv 1 foods.";
		}
		close;
	}

	if(cooking == 5){
		mes "[Chef's Apprentice]";
		mes "Hi there. Is there something";
		mes "that I can help you with?";
		next;
		menu "Buy Supplies",-, "Leave",L_Leave2;		

			mes "[Chef's Apprentice]";
			mes "Outdoor Cooking Set - 500z each.";
			mes " How many?";
			input @amount;
			if(@amount < 1) goto ER_Invalid;
			if(@amount > 100) goto ER_TooMuch;
			if(@amount*500 > Zeny) goto ER_Zeny;
			set Zeny, Zeny-(@amount*500);
			getitem 12125, @amount;//Items: Outdoor Cooker,

			next;
			mes "[Chef's Apprentice]";
			mes "Enjoy!";
			close;

		L_Leave2:
			mes "[Chef's Apprentice]";
			mes "Maybe next time.";
			close;

	}

	if(cooking == 6){

		mes "[Chef's Apprentice]";
		mes "Thank you for taking that";
		mes "pouch to my friend! Take";
		mes "this as a gift.";
		set cooking, 7;
		getitem 12126, 10;//Items: Home Cooking Set,
		next;
		mes "[Chef's Apprentice]";
		mes "Come back later if you";
		mes "need anything else!";
		close;

	}

	if(cooking == 7){
		mes "[Chef's Apprentice]";
		mes "Welcome back. What";
		mes "would you like to do?";
		next;
		menu "Buy Supplies",-, "Just visiting",L_Leave3;

				mes "[Chef's Apprentice]";
				mes "What would you like?";
				next;
				menu "Outdoor Cooking Set - 500z each",-, "Home Cooking Set - 1,000z",L_HomeCooking, "Never Mind",L_Leave3;

				set @price, 500;
				set @tool, 12125;
				goto L_Cooking_Tools4;

			L_HomeCooking:
				set @price, 1000;
				set @tool, 12126;

		L_Cooking_Tools4:
			mes "[Chef's Apprentice]";
			mes " How many?";
			input @amount;
			if(@amount < 1) goto ER_Invalid;
			if(@amount > 100) goto ER_TooMuch;
			if(@amount*@price > Zeny) goto ER_Zeny;
			set Zeny, Zeny-(@amount*@price);
			getitem @tool, @amount;

			next;
			mes "[Chef's Apprentice]";
			mes "Enjoy!";
			close;		

		L_Leave3:
			mes "[Chef's Apprentice]";
			mes "Come back to vist anytime!";
			mes "Have a good day.";
			close;
	}			
			
	mes "[Chef's Apprentice]";
	mes "Hi there. What can I";
	mes "do for you?";
	next;
	menu "Just looking around",-, "I want to learn to cook",ap_2;

		mes "[Chef's Apprentice]";
		mes "Feel free to look around";
		mes "as much as you like.";
		mes "Just don't disturb my";
		mes "teacher.";
		close;

	ap_2:
		if(baseLevel < 50){
			mes "[Chef's Apprentice]";
			mes "You should train a little";
			mes "more before trying something";
			mes "as hard as cooking";
			close;
		}

		mes "[Chef's Apprentice]";
		mes "So you want to learn to";
		mes "cook huh? Well, if you";
		mes "think you can handle it,";
		mes "just talk to my teacher.";
		next;
		mes "[Chef's Apprentice]";
		mes "Just don't forget your";
		mes "Chef's Hat or Sharle";
		mes "will yell at you.";
		set cooking, 1;
		close;

	ER_Zeny:
		next;
		mes "[Chef's Apprentice]";
		mes "You don't have enough zeny.";
		close;

	ER_TooMuch:
		next;
		mes "[Chef's Apprentice]";
		mes "You can't by that much at a time!.";
		mes "You don't have to buy it all at once.";
		close;
	ER_Invalid:
		next;
		mes "[Chef's Apprentice]";
		mes "You can't buy negative amounts of cooking";
		mes "equipment. Please buy a valid amount.";
		close;
}


payon.gat,209,127,4	script	Cooking Friend	88,{

	if(cooking == 5){
		mes "[Cooking Friend]";
		if(countitem(7432)) {
			mes "I see my friend sent you";
			mes "to give me something.";
			delitem 7432, 1;//Items: Leather Pouch,
			set cooking, 6;
		} else {
			mes "What? My friend sent you";
			mes "just to say Hello?";
		}
		mes "Thank you for your";
		mes "trouble. Tell him I said,";
		mes "Hello.";
		delitem 7432, 1;//Items: Leather Pouch,
		set cooking, 6;
		close;
	}
	
	mes "[Cooking Friend]";
	mes "Hello, how are you?";
	close;

}


prt_castle.gat,45,35,4	script	Sharle	886,{

	if(cooking == 7) goto L_Cooking_7;

	if(first_cooking == 1)goto FC_1;
	if(first_cooking == 2)goto FC_2;
	if(first_cooking == 3)goto FC_3;
	if(first_cooking == 4)goto FC_4;
	if(first_cooking == 5)goto FC_5;
	if(first_cooking == 6)goto FC_6;

	if(cooking == 1){
		if(getequipid(1) != 5026)goto L_uniform; //Item: Chef's Hat
		mes "[Sharle]";
		mes "So you want to learn how to cook?";
		next;
		menu "Yes",L_Cooking1_1, "No",L_Cooking1_2;
	}

	mes "[Sharle]";
	mes "What a great day to bake some";
	mes "wonderfull treats!";
	close;
		
	L_Cooking1_1:
		set first_cooking,rand(1,6);

		if(first_cooking == 1){
			FC_1:
				if(getequipid(1) != 5026)goto L_uniform; //Item: Chef's Hat

				set @item1, 577;
				set @item1a, 1;
				set @item2, 908;
				set @item2a, 10;
				set @item3, 1024;
				set @item3a, 1;
				set @food1, 12056;

				mes "[Sharle]";
				mes "Bring me:";
				mes "1 Grain, 10 Spawn, and";
				mes "1 Squid Ink";
				if(countitem(@item1) >= @item1a && countitem(@item2) >= @item2a && countitem(@item3) >= @item3a)goto FC_cook;
				next;
				close;
		}

		if(first_cooking == 2){
			FC_2:
				if(getequipid(1) != 5026)goto L_uniform; //Item: Chef's Hat

				set @item1, 518;
				set @item1a, 1;
				set @item2, 514;
				set @item2a, 2;
				set @item3, 501;
				set @item3a, 1;
				set @food1, 12061;

				mes "[Sharle]";
				mes "bring me:";
				mes "1 Honey, 2 Grapes, and";
				mes "1 Red Potion";
				if(countitem(@item1) >= @item1a && countitem(@item2) >= @item2a && countitem(@item3) >= @item3a)goto FC_cook;
				next;
				close;
		}

		if(first_cooking == 3){
			FC_3:
				if(getequipid(1) != 5026)goto L_uniform; //Item: Chef's Hat

				set @item1, 514;
				set @item1a, 3;
				set @item2, 501;
				set @item2a, 2;
				set @item3, 0;
				set @item3a, 0;
				set @food1, 12046;

				mes "[Sharle]";
				mes "Bring me:";
				mes "3 Grapes and 2 Red Potions";
				if(countitem(@item1) >= @item1a && countitem(@item2) >= @item2a && countitem(@item3) >= @item3a)goto FC_cook;
				next;
				close;
		}

		if(first_cooking == 4){
			FC_4:
				if(getequipid(1) != 5026)goto L_uniform; //Item: Chef's Hat

				set @item1, 942;
				set @item1a, 5;
				set @item2, 7031;
				set @item2a, 1;
				set @item3, 7457;
				set @item3a, 1;
				set @food1, 12066;

				mes "[Sharle]";
				mes "Bring me:";
				mes "5 Yoyo Tails, 1 Old Frying Pan, and";
				mes "1 Cooking Oil";
				if(countitem(@item1) >= @item1a && countitem(@item2) >= @item2a && countitem(@item3) >= @item3a)goto FC_cook;
				next;
				close;
		}

		if(first_cooking == 5){
			FC_5:
				if(getequipid(1) != 5026)goto L_uniform; //Item: Chef's Hat

				set @item1, 940;
				set @item1a, 5;
				set @item2, 7031;
				set @item2a, 1;
				set @item3, 7457;
				set @item3a, 1;
				set @food1, 12041;

				mes "[Sharle]";
				mes "Bring me:";
				mes "5 Grasshopper Legs, 1 Old Frying Pan, and";
				mes "1 Cooking Oil";
				if(countitem(@item1) >= @item1a && countitem(@item2) >= @item2a && countitem(@item3) >= @item3a)goto FC_cook;
				next;
				close;
		}

		if(first_cooking == 6){
			FC_6:
				if(getequipid(1) != 5026)goto L_uniform; //Item: Chef's Hat

				set @item1, 960;
				set @item1a, 10;
				set @item2, 511;
				set @item2a, 10;
				set @item3, 503;
				set @item3a, 1;
				set @food1, 12051;

				mes "[Sharle]";
				mes "Bring me:";
				mes "10 Nippers, 1 Green Herb, and";
				mes "1 Yellow Potion";
				if(countitem(@item1) >= @item1a && countitem(@item2) >= @item2a && countitem(@item3) >= @item3a)goto FC_cook;
				next;
				close;
		}

	L_Cooking1_2:
		mes "[Sharle]";
		mes "That's fine with me, come back";
		mes "when you want to cook.";
		close;

	FC_cook:
		next;
		delitem @item1, @item1a;
		delitem @item2, @item2a;
		delitem @item3, @item3a;
		set cooking, 2;

		mes "[Sharle]";
		mes "Great Job!";
		mes "You can now purchase";
		mes "Outdoor Cooking Sets.";
		getitem @food1, 1;
		set first_cooking, 0;
		close;

	L_Cooking_7:
		if(getequipid(1) != 5026)goto L_uniform; //Item: Chef's Hat

		mes "[Sharle]";
		mes "Would you like to check";
		mes "out a Cook Book?";
		next;
		menu "Yes",-, "No", s_2, "Return Book", s_3;

		mes "[Sharle]";
		mes "You'll have to give me back the";
		mes "first book I loaned you if you";
		mes "would like to check out a new book.";
		mes "Would you like to trade books?";
		next;
		menu "Yes",-, "No",s_2;

			mes "[Sharle]";
			mes "Which book would you like?";
			next;
			menu "Lv 1 Cookbook",b_1, "Lv 2 Cookbook",b_2, "Lv 3 Cookbook",b_3, "Lv 4 Cookbook",b_4, "Lv 5 Cookbook",b_5;

	b_1:
		mes "[Sharle]";
		mes "You will need:";
		mes "10 Pumpkins";
		next;
		if(countitem(535) < 10) goto need_items;//Items: Pumpkin,
		mes "And I see you have";
		mes "what you need!";
		next;
		
		delitem 535, 10;//Items: Pumpkin,
		set @checkout, 7472;
		goto b_trade;

	b_2:
		mes "[Sharle]";
		mes "You will need:";
		mes "5 well-baked cookies";
		next;
		if(countitem(538) < 5) goto need_items;//Items: Well-baked Cookie,
		mes "And I see you have";
		mes "what you need!";
		next;

		delitem 538, 5;//Items: Well-baked Cookie,
		set @checkout, 7473;
		goto b_trade;

	b_3:
		mes "[Sharle]";
		mes "You will need:";
		mes "5 Sushi";
		next;
		if(countitem(551) < 5) goto need_items;//Items: Sushi,
		mes "And I see you have";
		mes "what you need!";
		next;

		delitem 551, 5;//Items: Sushi,
		set @checkout, 7474;
		goto b_trade;

	b_4:
		mes "[Sharle]";
		mes "You will need:";
		mes "5 Dumplings";
		next;
		if(countitem(553) < 5) goto need_items;//Items: Dumpling,
		mes "And I see you have";
		mes "what you need!";
		next;

		delitem 553, 5;//Items: Dumpling,
		set @checkout, 7475;
		goto b_trade;

	b_5:
		mes "[Sharle]";
		mes "You will need:";
		mes "10 shoots";
		next;
		if(countitem(711) < 10) goto need_items;//Items: Shoot,
		mes "And I see you have";
		mes "what you need!";
		next;

		delitem 711, 10;//Items: Shoot,
		set @checkout, 7476;
		goto b_trade;

	need_items:
		mes "[Sharle]";
		mes "So come back when";
		mes "you have the items";
		mes "that you need.";
		close;

	b_trade:
		if(cook_book == 0){
			mes "[Sharle]";
			getitem @checkout,1;
			set cook_book, @checkout;
			mes "Good luck! I hope you learn a lot!";
			goto s_end;
		}


	delitem cook_book,1;
	getitem @checkout,1;
	set cook_book, @checkout;

	mes "[Sharle]";
	mes "If you find you need a new book,";
	mes "you're welcome to come back and";
	mes "trade. Have fun, and good luck!";
	close;

	s_2:
		mes "[Sharle]";
		mes "Well, I'm here whenever you'd like";
		mes "to learn. Just make sure you're";
		mes "ready.";
		close;

	s_3:
		if(cook_book == 0){
			mes "[Sharle]";
			mes "You haven't borrowed any books.";
			close;
		}

		set cook_book, 0;
		mes "[Sharle]";
		mes "Returning the book already?";
		mes "did you learn what you wanted";
		mes "to know?";
		close;

	L_uniform:
		mes "[Sharle]";
		mes "How dare you disrespect me";
		mes "by showing up out of uniform.";
		mes "Come back when you have your";
		mes "uniform on.";
		close;
		
}
