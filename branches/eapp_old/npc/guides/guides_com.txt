//===== eAthena Script ======================================= 
//= Comodo Guides
//===== By: ================================================== 
//= kobra_k88
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= eAthena  0.5.2 +
//===== Description: ========================================= 
//=
//===== Additional Comments: ================================= 
//= Fully working
//= v1.1 Now using duplicate command.
//============================================================




// East -------------------------------------------------------------------------------------
comodo.gat,322,178,4	script	Comodo Guide#1::Comodo_Guide	724,{
	mes "[Comodo Guide]";
	mes "The city of Dreams and Fantasy, where the night life continues 24 hours a day, we welcome you to the land of Comodo!";
	next;
	mes "[Comodo Guide]";
	mes "I know the area very well.  If you need directions, ask me for asistance anytime.";
M_Menu:
	next;
	menu "View Buildings",M_1, "Notice",M_2, "Cancel",M_End;

	M_1:
		mes "[Comodo Guide]";
		mes "Do you want me to leave indications on the Mini-Map?";
		next;
		menu "No Thanks",sM_1a,"Yes Please",sM_1b;
		
		sM_1a:
			set @COMPASS_CHECK,0;
			goto L_Cont;

		sM_1b:
			set @COMPASS_CHECK,1;

		L_Cont:
		mes "[Comodo Guide]";
		mes "Please choose a building to view.";
		next;
	sM_Menu:
		mes "[Comodo Guide]";
		menu	"^FF8000Casino^000000",sM_1, "^34B4A0Hula dancing stage^000000",sM_2, "Weapons and Armor Shop",sM_3, "Tool store",sM_4,
			"Tourist Shop",sM_5, "^F1BE0EKafra Corp. Western Branch^000000",sM_6, "^FF5555Chief's House^000000",sM_7, "Pub",sM_8,
			"^00B200Camp Grounds^000000",sM_9, "Clear all indications from mini-map.",sM_10, "End Conversation",M_End;

		sM_1:
			mes "The heart of Comodo's night life!";
			mes "A heaven of rest for tired travelers!";
			mes "The Comodo Casino ~!";
			viewpoint 1,140,98,1,0xFF8000;
			goto sL_Wipe;
		sM_2:
			mes "Hula Hula - Hula Hula";
			next;
			mes "[Comodo Guide]";
			mes "When you become more comfortable performing on stage, you should consider changing your job to a '^FF0000Dancer^000000'.";
			viewpoint 1,188,168,2,0x34B4A0;
			goto sL_Wipe;
		sM_3:
			mes "Here in Comodo, you can find unique weapons and armor only found in this area.";
			viewpoint 1,266,70,3,0xFF00FF;
			goto sL_Wipe;
		sM_4:
			mes "Here in Comodo, you can find usefull items only found in this area.";
			mes "'Comodo Tool Store'";
			viewpoint 1,86,128,4,0xFF00FF;
			goto sL_Wipe;
		sM_5:
			mes "Here in Comodo, you can find unique gifts only found in this area.";
			mes "'Comodo Tourist Shop'";
			viewpoint 1,298,124,5,0x00FFFF;
			goto sL_Wipe;
		sM_6:
			mes "Kafra Corp.'s Western Branch center is located here in Comodo, offering you superior service.";
			viewpoint 1,136,202,6,0xF1BE0E;
			goto sL_Wipe;
		sM_7:
			mes "This is the house where the Chief of Comodo resides.";
			mes "If you are one who posseses a serious interest in Comodo, you can meet the Chief in person.";
			mes "I think that would be okay with him.";
			viewpoint 1,114,294,7,0xFF5555;
			goto sL_Wipe;
		sM_8:
			mes "The pub is a place where tourists from all over the world come to converse and exchange ideas.";
			viewpoint 1,166,298,8,0x6666FF;
			goto sL_Wipe;
		sM_9:
			mes "Freinds and family often come to the Comodo Camp Grounds for the mermorable experiences.";
			mes "It is also the place to experience Comodo's unique, ethinic BBQ!!";
			viewpoint 1,210,308,9,0x00B200;
			goto sL_Wipe;
		sM_10:
			set @COMPASS_CHECK, 2;

		sL_Wipe:
			if(@COMPASS_CHECK != 2) next;	// avoids a double 'next' when 'wipe indications' is chosen
			if(@COMPASS_CHECK == 1) goto sM_Menu;
			viewpoint 2,140,98,1,0xFF8000;
			viewpoint 2,188,168,2,0x34B4A0;
			viewpoint 2,266,70,3,0xFF00FF;
			viewpoint 2,86,128,4,0xFF00FF;
			viewpoint 2,298,124,5,0x00FFFF;
			viewpoint 2,136,202,6,0xF1BE0E;
			viewpoint 2,114,294,7,0xFF5555;
			viewpoint 2,166,298,8,0x6666FF;
			viewpoint 2,210,308,9,0x00B200;
			if (@COMPASS_CHECK == 0) goto sM_Menu;
			mes "All indications have been removed";
			close;

	M_2:
		mes "[Comodo Guide]";
		mes "We upgraded the Location Guide to the Newest Digital Style.  We hope you like this gorgeous new system.";
		next;
		mes "[Comodo Guide]";
		mes "Don't forget to refer to the Mini-Map on the Upper-Right corner of your screen.";
		next;
		mes "[Comodo Guide]";
		mes "If you can't see the Mini-Map, just hit ^0000ff'ctrl+tab'^000000 or Click the ^0000ff'map'^000000 button in the Basic Information Window";
		mes "Remeber to use the ^ff0000+,-^000000 buttons to adjust the map to your liking.";
		goto M_Menu;

	M_End:
		mes "[Comodo Guide]";
		mes "Because Comodo is located in a cave, there is absolutely no sunlight.  It is dark 24 hours a day!";
		mes "The residents of Comodo are using this unusual circumstance to draw tourists to our area...";
		close;
}

// North ----------------------------------------------------------------------------------------
comodo.gat,176,350,4	duplicate(Comodo_Guide)	Comodo Guide#2	724
// West -----------------------------------------------------------------------------------------
comodo.gat,37,219,4	duplicate(Comodo_Guide)	Comodo Guide#3	724

