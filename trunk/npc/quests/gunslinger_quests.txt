//===== By: ==================================================
//= Playtester
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= SVN eA
//===== Description: =========================================
//= Gunslinger guns and bullet quests
//===== Additional Comments: =================================
//= 1.0 Only Garrison Gun done yet [Playtester]
//= 1.1 Added Garrison[2]
//============================================================


//===================== Garrison =====================================================

que_ng.gat,182,85,3	script	Mr.Garrison	109,{
	set @npcname$,"[Mr.Garrison]";

	mes @npcname$;
	if(countitem(13105)>0) goto L_Already;
	if(countitem(13104)>0) goto L_Slot;
	mes "I have invented a new Revolver!";
	mes "I think I call it... Garrison.";
	next;
	mes "[" +strcharinfo(0) + "]";
	mes "Very innovative name really.";
	next;
	if(BaseJob != Job_Gunslinger || BaseLevel < 55){
		mes @npcname$;
		mes "I don't think you have enough";
		mes "knowledge of guns to see its";
		mes "true beauty.";
		close;
	}
	mes @npcname$;
	mes "You look like you're an expert";
	mes "in guns, aren't you?";
	next;
	mes "[" +strcharinfo(0) + "]";
	mes "Yeah you could say so.";
	next;
	mes @npcname$;
	mes "So, do you want me to make a";
	mes "^0000FFGarrison^000000 for you?";
	next;

	switch(select("Yes, please.","Nah, I'll stick with my gun."))
	{

		case 1:
			mes @npcname$;
			mes "Great! But first you need to";
			mes "bring me the materials I need";
			mes "to create one.";
			next;
			mes @npcname$;
			mes "This is what I need:";
			mes "^FF000050 Steels^000000,";
			mes "^FF00003 Eluniums^000000,";
			mes "^FF00001 Oridecon^000000,";
			mes "^FF000050 Coals^000000,";
			mes "^FF000020 Rusty Screws^000000 and";
			mes "^FF000030000 Zeny^000000.";
			next;
			mes @npcname$;
			if ( (countitem(999)<50) || (countitem(985)<3) || (countitem(984)<1) ||(countitem(1003)<50) || (countitem(7317)<20) || (Zeny < 30000) ) {
				mes "Come back when you have the materials.";
				close;
			}
			mes "I see you came prepared.";
			next;
			delitem 999,50;
			delitem 985,3;
			delitem 984,1;
			delitem 1003,50;
			delitem 7317,20;
			set Zeny,Zeny-30000;
			mes @npcname$;
			mes "Give me just a minute...";
			mes "...";
			mes "...";
			next;
			getitem 13104,1;
			mes @npcname$;
			mes "It's done. Here, take this";
			mes "wonderful gun!";
			close;

		case 2:
			mes @npcname$;
			mes "Well, guess a true Gunslinger";
			mes "will never part from his";
			mes "favorite gun, heh.";
			close;
	}

L_Slot:
	mes "Oh, I see you have one of";
	mes "my high-quality guns.";
	next;
	mes @npcname$;
	mes "Now that I look at it, I";
	mes "think I could improve it";
	mes "some more.";
	next;
	mes @npcname$;
	mes "How about I add a ^0000FFslot^000000";
	mes "to it? Be careful, though.";
	mes "If you put a card into it";
	mes "already, it will be lost";
	mes "in the process.";
	next;
	switch(select("Yes, please add a slot.","Nah, I keep it as it is now."))
	{

		case 1:
			mes @npcname$;
			mes "Great! But first you need to";
			mes "bring me the materials I need";
			mes "to improve it.";
			next;
			mes @npcname$;
			mes "This is what I need:";
			mes "^FF000010 Steels^000000,";
			mes "^FF00001 Eluniums^000000,";
			mes "^FF000010 Emveretarcon^000000,";
			mes "^FF000030 Coals^000000,";
			mes "^FF000010 Rusty Screws^000000 and";
			mes "of course your ^FF0000Garrison^000000.";
			next;
			mes @npcname$;
			if ( (countitem(999)<10) || (countitem(985)<1) || (countitem(1011)<10) ||(countitem(1003)<30) || (countitem(7317)<10) || (countitem(13104)<1) ) {
				mes "Come back when you have the materials.";
				close;
			}
			mes "I see you came prepared.";
			next;
			delitem 999,10;
			delitem 985,1;
			delitem 1011,10;
			delitem 1003,30;
			delitem 7317,10;
			delitem 13104,1;
			mes @npcname$;
			mes "Give me just a minute...";
			mes "...";
			mes "...";
			next;
			getitem 13105,1;
			mes @npcname$;
			mes "It's done. Now it is";
			mes "even better than before.";
			mes "Put it to good use.";
			close;

		case 2:
			mes @npcname$;
			mes "Okay, come back when you";
			mes "changed your mind.";
			close;
	}

L_Already:
	mes "So how do you like";
	mes "my gun?";
	close;
}