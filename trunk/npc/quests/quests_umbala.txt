//===== eAthena Script ======================================= 
//= Quest NPCs related to Umbala
//===== By: ================================================== 
//= sabernet09 & eAthena Team
//===== Current Version: ===================================== 
//= 1.7
//===== Compatible With: ===================================== 
//= eAthena 7.15 +
//===== Description: ========================================= 
//= Umbalian Language Quest, Umbala Skeletal Gate Quest, Umbala Event,
//= Create Essence/Dismantle Stone
//
//Phase1.Learning the Language
//1	(Elder)First discussion
//2	(Elder)Asking about learning the language
//3	(Elder)Final step (understanding NPC speech)
//------------------------------------------
//Phase2.Create Essence/Dismantle Stone
//4	(Shaman)Get permission to speak with her from the chief
//5	(Elder)Ask about conditions needed to be fulfilled to get permission
//6	(Elder)Get permission
//7	(Shaman)Use the shaman to create essences and dismantle elemental stones.
//===== Additional Comments: ================================= 
//= fixed by x[tsk], Lupus, PoW
//= Quest completion is now registered
//= 1.1 fixed Poporing Egg ID -> Poring Egg, fixed condition
//= 1.2-1.5 fixed some Umbalian Language quests:
//= 	- Now if Umbalian Chief tell u about meaning of masks
//=	  you can interact with activated Sphinx Mask Quest
//=	- with Wise Man Fabius you can learn Umbalian Language
//=	  in alternative way.
//= 1.6 Fixed bugs [Lupus]
//= 1.7 Moved Umbala Event, Create Essence/Dismantle Stone, and Skeletal Gate from npc/cities/umbala.txt [Evera]
//= TODO: should I add a jRO Werewolf Quest? 8)
//============================================================ 



//===========================================================================
//this quest is related to Umbalian Chief
morocc.gat,140,156,5	script	Turban Thief	58,{
	if(mask_q == 0) goto AWAY;
	if(mask_q == 2) goto NoBus;
	mes "[Turban Thief]";
	mes "E'llo mah frien, would I interesst tu with this rare mask?  Its value I assure you is real mah frien.  Tis manific!";
	next;
	mes "[Turban Thief]";
	mes "Wah? O ho ho ho, so you know thaz I steal dis from those savages no?  Well I won'tz give it back.  But I will for a pricez... wat you say?";
	next;
	menu "Pay 1,000,000z",-,"No deal",NoGo;

		if(Zeny < 1000000) goto NotEnough;
		set Zeny,Zeny-1000000;
		mes "[Turban Thief]";
		mes "O ho ho, its a deal then!";
		getitem 7114,1;//Items: Sphinx Mask,
		set mask_q,2;
		close;

	NoGo:
		mes "[Turban Thief]";
		mes "Ahh a business man are you no?  Fine, how about...";
		next;
		menu "Pay 750,000",-,"Nope lower",NoGoB;

			if(Zeny < 750000) goto NotEnough;
			set Zeny,Zeny-750000;
			mes "[Turban Thief]";
			mes "It is a done deal, no refunds!";
			getitem 7114,1;//Items: Sphinx Mask,
			set mask_q,2;
			close;
		NoGoB:
			mes "[Turban Thief]";
			mes "Hmmm... you drive a hard bargain, ok... my final offer...";
			next;
			menu "Pay 500,000",-,"It can go lower than that.",NoGoC;

				if(Zeny < 500000) goto NotEnough;
				set Zeny,Zeny-500000;
				mes "[Turban Thief]";
				mes "Alright, here you go then...";
				getitem 7114,1;//Items: Sphinx Mask,
				close;
			NoGoC:
				next;
				mes "[Turban Thief]";
				mes "Ack!  Forgez it!  I can do bettaz en elsez where!";
				set mask_q,2;
				close;
	NoBus:
		mes "[Turban Thief]";
		mes "You have no more business with me, go away!";
		close;
	AWAY:
		mes "[Turban Thief]";
		mes "What do you wan te withz me?  Be gone!";
		close;
	NotEnough:
		mes "[Turban Thief]";
		mes "Are youz playin wit me?  You don't have ze money!";
		close;
}
		
//======================================================================================//
//				Umbala Language Quest (optional)
//======================================================================================//

prontera.gat,145,290,5	script	Mason	742,{
	mes "[Mason]";
	if(umb_lang==0) goto L_NOT_GOT_QUEST;
	if(umb_lang==2 || umb_mason==1) goto L_AGAIN;

	mes "Hey there, I came here to Prontera in search of an old lady who can remove cards from slots. Do you know where I can find her?";
	menu "Yes",-, "No",M_NO;

	mes "Oooo, could you please tell me where she's at? I'd be really appreciative.";
	menu "Swordsman Association",-, "Sanctuary",M_Boo1,"Prontera Chivalry",M_Boo2,"Trading Post",M_Boo3,"Kit Shop",M_Boo4,"Inn",M_Boo5,"Armory",M_DONE,"Library",M_Boo6,"Job Agency",M_Boo7,"Prontera Castle",M_Boo8,"City Hall",M_Boo9;

	next;
	mes "[Mason]";
	mes "Hmm... I dunno why she would be there... guess I'll take a look.";
	close;
M_Boo1:
	next;
	mes "[Mason]";
	mes "Wow I never knew she was a religious person... I'll see to it thanks.";
	close;
M_Boo2:
	next;
	mes "[Mason]";
	mes "Hmm.... are you sure about that?";
	close;
M_Boo3:
	next;
	mes "[Mason]";
	mes "Why would she need to go there? I think you might be mistaken.";
	close;
M_Boo4:
	next;
	mes "[Mason]";
	mes "The only thing she'd need at a Kit shop is medicine for her rheumatism. She's probably long gone from there by now.";
	close;
M_Boo5:
	next;
	mes "[Mason]";
	mes "I don't think she needs to be there, she LIVES here in Prontera.";
	close;
M_Boo6:
	next;
	mes "[Mason]";
	mes "Ah I see, if she's studying then I don't want to bother her. Thanks anyways.";
	close;
M_Boo7:
	next;
	mes "[Mason]";
	mes "Job Agency?  I don't think so.  Don't lead me all around Prontera please! She's already a teacher here.";
	close;
M_Boo8:
	next;
	mes "[Mason]";
	mes "The castle eh, I don't want to step in there... just doesn't feel like my style. I don't like that noble feeling of importance type of thing.";
	close;
M_Boo9:
	next;
	mes "[Mason]";
	mes "O wow, did she become an important part of Prontera? I'd think she's too busy for me to visit. I'll try again later.";
	close;
M_DONE:
	next;
	mes "[Mason]";
	mes "Ah, I heard that her class was somewhere around there... thanks a lot! Here's a lil something for your help.";
	getitem 7117,1;//Items: Torn Spell Book,
	set umb_mason,1;
	close;

L_AGAIN:
	emotion e_thx;
	mes "Yay, I found her! Thank you very much for you help!";
	close;

L_NOT_GOT_QUEST:
	mes "Hi! I came here to Prontera in search of an old lady... But...";
	close;
M_NO:
	mes "Oh.. Ok.. Thanks anyways.";
	close;
}

aldebaran.gat,153,212,1	script	Mojo	740,{
	mes "[Mojo]";
	if(umb_lang!=1) goto L_NOT_GOT_QUEST;
	mes "Oh no!  I lost my teacher's ^FF0000old magic book^000000!";
	mes "What am I going to do... those dumb Bathory...";
	if(umb_lang==1) set umb_mojo,1;
	close;
L_NOT_GOT_QUEST:
	emotion e_what;
	mes "I lost my teacher's book!  What am I going to do...";
	close;
}

amatsu.gat,267,189,4	script	Marie	744,{
	mes "[Marie]";
	if(umb_lang==0) goto L_NOT_GOT_QUEST;
	if(umb_lang==2 || umb_marie==1) goto L_AGAIN;
	if(countitem(9001)>0 && countitem(643)>0) goto L_DONE;//Items: Poring Egg, Pet Incubator,
	mes "Hello, is there something you need?";
	next;
	menu "No nothing at all.",M_NO,"Your teacher sent me.",-;

	mes "[Marie]";
	mes "What does he want from me?";
	emotion e_what;
	next;
	mes "[Marie]";
	mes "Oh wait, I see... he wants his reference scroll back am I right? Well, I'm kinda still using it buuut... I'll give it back if you can gimme a poring egg and an incubator. Those things are so cute!";
	close;

L_NOT_GOT_QUEST:
	emotion e_what;
	mes "Hi! What do you want from me?";
	close;

M_NO:
	mes "[Marie]";
	mes "Hrrmm okay? Cya then..";
	close;

L_DONE:
	mes "Heyyy there, you have a poring egg and the pet incubator to go with it! Are those for me?";
	menu "Yep!",-, "Sorry, no.",M_DOH;

	mes "[Marie]";
	if(countitem(9001)==0 && countitem(643)==0) goto M_DOH;//Items: Poring Egg, Pet Incubator,
	mes "Yatta!  Here's a lil something in return.";
	delitem 9001,1;//Items: Poring Egg,
	delitem 643,1;//Items: Pet Incubator,
	getitem 7118,1;//Items: Torn Scroll,
	set umb_marie,1; //got Torn Scroll
	close;

M_DOH:
	mes "Oh.. pooo... sigh...";
	close;

L_AGAIN:
	emotion e_thx;
	mes "Yay! I love Porings!";
	close;
}

yuno.gat,157,366,2	script	Wise Man Fabius	743,{
	mes "[Fabius]";
	if(event_umbala >= 3) set umb_lang,2; //You already learnt Umbalian language from Umbalian Chief. So this Quest is disabled
	if(umb_lang<2) goto L_QUEST;
	mes "You speak Umbalian fluently! So you will never forget the language of Umbalians!";
	close;
L_QUEST:
	if((countitem(1006) < 1) || (countitem(7118) < 1) || (countitem(7117) < 1)) goto L_DONE;//Items: Old Magic Book, Torn Scroll, Torn Spell Book,
	delitem 1006,1;	//Items: Old Magic Book,
	delitem 7118,1;	//Items: Torn Scroll,
	delitem 7117,1;//Items: Torn Spell Book,
	emotion e_thx;
	mes "Ah, you've done well in retrieving my references. These artifacts holds information about ruins around the world, from the burning hell of the Magma Dungeon, to the strange eerie Hidden Forest of Yggsadril.";
	next;
	mes "[Fabius]";
	mes "But enough talk, you're just eager to get your reward aren't you?  Well it may not sound much, but my reward for you is knowledge...";
	next;
	mes "[Fabius]";
	mes "Actually it is knowledge of the Umbalian people. They are connected to this world of Midgard in some special way, we just can't confirm it yet. There is a theory that they may be like the elves and protect the ancient tree of Yggsadril.";
	next;
	mes "[Fabius]";
	mes "And I, a great wise man of Yuno, will teach you their language!";
	next;
	mes "^0000FF Learned the language of Umbalians! ^000000";
	set umb_lang,2;
	//clear subquests
	set umb_marie,0;
	set umb_mason,0;
	set umb_mojo,0;
	//make you understand Umbalian people
	//it's an alternative way to learn Umbalian Language
	if(event_umbala < 3) set event_umbala,3;
	close;
L_DONE:
	set umb_lang,1; //got Quest
	mes "Argh! My three students ran off with my references! Hey you! Can you get them back for me? I'll gladly reward you.";
	next;
	mes "[Fabius]";
	mes "From what I know, they're in different seperate towns making something out of themselves.";
	if(umb_marie==0 && umb_mason==0 && umb_mojo==0) goto L_NOT_MET;
	if(umb_marie==1) mes "Oh... You found Marie in Amatsu.";
	if(umb_mojo==1) mes "You saw Mojo in Aldebaran?";
	if(umb_mason==1) mes "So... Mason's in Prontera now... Well...";
	if(umb_marie==0) mes "I wonder where is our sea girl Marie?";
	if(umb_mojo==0) mes "Mojo should be somewhere near water...";
	if(umb_mason==0) mes "Where'd Mason go then...";
	mes "Oh yeah, tell them I sent you";
	close;
L_NOT_MET:
	mes "Oh yeah, tell them I sent you, their names are Marie, Mason and Mojo.";
	close;
}

//========================================================================================================================\\
//                       Little Quest NPC to enter the dungeon
//                                Fixed by Akaru
//========================================================================================================================\\
umbala.gat,65,253,4	script	Tree Guardian	752,{ 
	mes "[Tree Guardian]"; 
	mes "Stop, stranger, here is the entrance to the ancient tree!"; 
	next; 
	mes "[Tree Guardian]"; 
	mes "I cannot allow you to enter, you look pretty weak!"; 
	next; 
L_MENU:
	menu "Talk about tree",-, "Talk about Umbala",M_TALKUMB, "Enter Dungeon",M_DUN, "Leave",M_Leave; 

	mes "[Tree Guardian]"; 
	mes "The tree is an ancient relique of our culture!"; 
	next; 
	mes "[Tree Guardian]"; 
	mes "But recently some monsters overwhelmed the tree and are now"; 
	mes "slowly killing the tree...like a poison...from inside.."; 
	next; 
	goto L_MENU;
M_TALKUMB: 
	mes "[Tree Guardian]"; 
	mes "We don't have many visitors in our small town.."; 
	next; 
	mes "[Tree Guardian]"; 
	mes "Most people don't find us or are just afraid of us!"; 
	next; 
	mes "[Tree Guardian]"; 
	mes "And now, we are even more unpopular, because of that damn beast!"; 
	next; 
	menu "What beast?",- ,"Leave",M_Leave; 

	mes "[Tree Guardian]"; 
	mes "The Werewolf of course!!"; 
	next; 
	mes "[Tree Guardian]"; 
	mes "He is sneaking around our town and killing our soldiers at night!"; 
	next; 
	mes "[Tree Guardian]"; 
	mes "Hey...if you could get rid of that beast!"; 
	next; 
	mes "[Tree Guardian]"; 
	mes "I'll let you in!"; 
	next; 
	mes "[Tree Guardian]"; 
	mes "From now on we can live in peace again, thanks to you!"; 
	next; 
	mes "[Tree Guardian]"; 
	mes "You may enter the dungeon now!"; 
	set umbdun,1;
	close; 
M_DUN: 
	mes "[Tree Guardian]"; 
	if(umbdun != 1) mes "We do not permit weak people to enter!";
	if(umbdun != 1) close;
	mes "Come on in, brave warrior!"; 
	warp "um_dun01.gat",42,30; 
M_Leave:  
	close; 
}

//==============================================================================
//Skeletal Gate
//==============================================================================
umbala.gat,221,193,1	script	 #Skeletal Gate	111,{
	if(event_umbala==7) goto LwarpNoText;
	set @ryumon,0;
	mes "^3355FFThe gate is shaped like a skeleton";
	mes "You cannot pass this point as the gate is";
	mes "locked.  You hear sounds coming from the";
	mes "inside.^000000";
	next;
	menu "Examine the skeleton",-,"Nothing",Lend;
		mes "^3355FFWhen you examine the gate";
		mes "carefully, you notice that there";
		mes "is a hole about the size of a gemstone";
		mes "on the left side of the gate about where";
		mes "the skeleton's left eye should be.  There is";
		mes "a matching hole on the other side.^000000";
		next;
		mes "^3355FFWhat you would like to";
		mes "to do with the left socket?^000000";
		next;
		menu "Nothing",-,"Insert a Blue Gemstone",L1_2,"Insert a Yellow Gemstone",L1_3,"Insert a Red Gemstone",L1_4;
			mes "^3355FFNothing was inserted into the left socket.^000000";
			next;
			goto LRight;
		L1_2:
			//Blue
			if(countitem(717) < 1) goto L1_2_e;//Items: Blue Gemstone,
				mes "^3355FFYou inserted a Blue Gemstone into the";
				mes "left socket.^000000";
				next;
				delitem 717,1;//Items: Blue Gemstone,
				getitem 717,1;//Items: Blue Gemstone,
				set @ryumon,1;
				mes "^3355FFA rolling sound can be heard, and";
				mes "the gemstone comes out of the skeleton's                                 mouth.^000000";
				next;
				goto LRight;
		L1_3:
			//Yellow
			if(countitem(715) < 1) goto L1_3_e;//Items: Yellow Gemstone,
				mes "^3355FFYou inserted a Yellow Gemstone into the";
				mes "left socket.^000000";
				next;
				delitem 715,1;//Items: Yellow Gemstone,
				getitem 715,1;//Items: Yellow Gemstone,
				set @ryumon,2;
				mes "^3355FFA rolling sound can be heard, and";
				mes "the gemstone comes out of the skeleton's                                 mouth.^000000";
				next;
				goto LRight;
		L1_4:
			//Red
			if(countitem(716) < 1) goto L1_4_e;//Items: Red Gemstone,
				mes "^3355FFYou inserted a Red Gemstone into the";
				mes "left socket.^000000";
				next;
				delitem 716,1;//Items: Red Gemstone,
				getitem 716,1;//Items: Red Gemstone,
				set @ryumon,3;
				mes "^3355FFA rolling sound can be heard, and";
				mes "the gemstone comes out of the skeleton's                                 mouth.^000000";
				next;
				goto LRight;
			L1_2_e:
				mes "^3355FFYou don't have a Blue Gemstone.";
				goto L1_e;
			L1_3_e:
				mes "^3355FFYou don't have a Yellow Gemstone.";
				goto L1_e;
			L1_4_e:
				mes "^3355FFYou don't have a Red Gemstone.";
			L1_e:
				mes "Therefore, nothing was inserted into";
				mes "the left socket.^000000";
				next;
	LRight:
		mes "^3355FFWhat about the right";
		mes "socket?^000000";
		next;
		menu "Nothing",-,"Insert a Blue Gemstone",L2_2,"Insert a Yellow Gemstone",L2_3,"Insert a Red Gemstone",L2_4;
			mes "^3355FFNothing was inserted into the right socket.^000000";
			next;
			goto Lfin;
		L2_2:
			//Blue
			if(countitem(717) < 1) goto L2_2_e;//Items: Blue Gemstone,
				mes "^3355FFYou inserted a Blue Gemstone into the";
				mes "right socket.^000000";
				next;
				delitem 717,1;//Items: Blue Gemstone,
				getitem 717,1;//Items: Blue Gemstone,
				set @ryumon,@ryumon+10;
				mes "^3355FFA rolling sound can be heard, and";
				mes "the gemstone comes out of the skeleton's                                 mouth.^000000";
				next;
				goto Lfin;
		L2_3:
			//Yellow
			if(countitem(715) < 1) goto L2_3_e;//Items: Yellow Gemstone,
				mes "^3355FFYou inserted a Yellow Gemstone into the";
				mes "right socket.^000000";
				next;
				delitem 715,1;//Items: Yellow Gemstone,
				getitem 715,1;//Items: Yellow Gemstone,
				set @ryumon,@ryumon+20;
				mes "^3355FFA rolling sound can be heard, and";
				mes "the gemstone comes out of the skeleton's                                 mouth.^000000";
				next;
				goto Lfin;
		L2_4:
			//Red
			if(countitem(716) < 1) goto L2_4_e;//Items: Red Gemstone,
				mes "^3355FFYou inserted a Red Gemstone into the";
				mes "right socket.^000000";
				next;
				delitem 716,1;//Items: Red Gemstone,
				getitem 716,1;//Items: Red Gemstone,
				set @ryumon,@ryumon+30;
				mes "^3355FFA rolling sound can be heard, and";
				mes "the gemstone comes out of the skeleton's                                 mouth.^000000";
				next;
				goto Lfin;
			L2_2_e:
				mes "^3355FFYou don't have a Blue Gemstone.";
				goto L2_e;
			L2_3_e:
				mes "^3355FFYou don't have a Yellow Gemstone.";
				goto L2_e;
			L2_4_e:
				mes "^3355FFYou don't have a Red Gemstone.";
			L2_e:
				mes "Therefore, nothing was inserted into";
				mes "the right socket.^000000";
				next;
			Lfin:
				mes "^3355FF..........^000000";
				next;
				mes "^3355FF...................";
				mes ".....................^000000";
				next;
				mes "^3355FF.......................";
				mes ".........................";
				mes ".........................^000000";
				next;
				if(event_umbala < 6) goto LSecondCheck;
					if(@ryumon == 33) goto Lwarp;
				LSecondCheck:
				if(event_umbala >= 6) goto LNewEnd;
					if(@ryumon == 13) goto Lwarp;
					LNewEnd:
						mes "^3355FFNothing happened.";
						mes "You hear a faint laugh, but";
						mes "decide that it's all in";
						mes "your head.^000000";
						close;
				Lwarp:
					mes "^3355FFA rumbling sound can be";
					mes "heard as the gate opens.";
					mes "As you step across the threshold.";
					mes "you feel as if you're walking in";
					mes "into the arms of death itself.^000000";
					next;
				LwarpNoText:
					warp "um_in.gat",32,71;
					close;
Lend:
	mes "^3355FFAfter hitting the gate a few";
	mes "times with no result, you decide to give";
	mes "up.^000000";
	close;
}

//==============================================================================
//Umbala Event
//==============================================================================
um_in.gat,39,122,5	script	Utan Tribal Elder	784,{
	if(event_umbala == 1) goto LStart2;	
	if(event_umbala == 2) goto LStart3;
	if(event_umbala == 3) goto LStart4;
	if(event_umbala == 4) goto LStart5;
	if(event_umbala == 5) goto LStart6;
	if(event_umbala == 6) goto LStart4;
	if(event_umbala == 7) goto LStart4;
	mes "[Karukatan]";
	mes "Aha, You're new to the Utan";
	mes "family's little village, aren't";
	mes "you?  I would remember such a face";
	mes "had I met you before.";
	next;
	mes "[Karukatan]";
	mes "We know there is a wide world around";
	mes "us, but the there is something to";
	mes "be appreciated in the simple life";
	mes "we lead here.";
	next;
	mes "[Karukatan]";
	mes "All decisions in the village are made";
	mes "by me or with my input";
	next;
	mes "[Karukatan]";
	mes "You want to know how I speak your";
	mes "language when it seems no one else in";
	mes "the village does?  A fair question.";
	mes "I picked up your tongue from the";
	mes "adventurers who sometimes pass";
	mes "through this village.";
	next;
	mes "[Karukatan]";
	mes "Some outsiders have also settled near here";
	mes "over time.  In exchange for information";
	mes "about their customs and culture, I";
	mes "teach them the language and customs";
	mes "of the Utan tribe.";
	next;
	mes "[Karukatan]";
	mes "If you want to learn our language,";
	mes "you must immerse yourselves in";
	mes "our customs and cultural traditions.";
	mes "You should even try and look as much";
	mes "like us as you can.";
	next;
	mes "[Karukatan]";
	mes "I hope you're worthy of my confidence.";
	mes "Some members of the tribe have expressed";
	mes "the sentiment that welcoming outsiders";
	mes "is dangerous to the village.";
	next;
	mes "[Karukatan]";
	mes "When you want to begin learning the";
	mes "language, come and visit me.  Don't";
	mes "forget to make yourself appear as";
	mes "much like us as you can.";
	set event_umbala,1;
	close;
LStart2:
	mes "[Karukatan]";
	mes "Ahh, have you looked around the";
	mes "village a bit?  Did other people";
	mes "from the tribe hide from you when";
	mes "they saw you?  Don't worry.  They";
	mes "were just surprised to see a new face.";
	next;
	mes "[Karukatan]";
	mes "Hmm?  You need something from me?";
	mes "How can I make your stay in the village";
	mes "more pleasant?";
	next;
	menu "I want to learn the tribal language",-,"Umbaumbaba?",L2,"Forget it",L3;
		if ((getequipisequiped(9)>0) && (getequipisequiped(10)>0)) goto L1_mask;
		mes "[Karukatan]";
		mes "Well, in order to understand our";
		mes "language, you need to assimilate";
		mes "our cultural values, too.  Trying to";
		mes "understand a language in the context of an";
		mes "alien culture is foolish, right?";
		next;
		mes "[Karukatan]";
		mes "Besides, if the tribesmen in the";
		mes "village fear you, how will you";
		mes "ever get them to speak with you?";
		mes "You should try making yourself";
		mes "look like us in order to be more";
		mes "accepted.";
		close;
	L1_mask:
		mes "[Karukatan]";
		mes "What a cute mask.  The Utan tribe";
		mes "appreciates its traditional masks";
		mes "because sometimes by hiding your";
		mes "face, you can express more genuine";
		mes "feelings.";
	//Activate Sphinx Mask Quest (optional)
		set mask_q,1;
		next;
		mes "[Karukatan]";
		mes "Over time, the mask itself";
		mes "has come to symbolize trust among";
		mes "the members of the Utan tribe.";
		next;
		mes "[Karukatan]";
		mes "Now that you're prepared, I will";
		mes "begin teaching you the Utan";
		mes "language as promised.  First,";
		mes "you need to make some preparations,";
		mes "however...";
		next;
		mes "[Karukatan]";
		mes "First, I need some paper.  With";
		mes "paper, I can write down the words";
		mes "I intend to teach.";
		next;
		mes "[Karukatan]";
		mes "I need ^3377FF10x Oil Paper^000000, and ^3377FF5x Slick Paper^000000.";
		mes "I also need something to write with.";
		mes "of course.";
		mes "For that, ^3377FF1x Squid Ink^000000 and ^3377FF1x Feather of Birds^000000";
		mes "will do nicely.";
		next;
		mes "[Karukatan]";
		mes "Once you get these, I can start";
		mes "teaching you the language.";
		set event_umbala,2;
		close;
	L2:
		mes "[Karukatan]";
		mes "Haha.  Only imitating the sound";
		mes "of the words is useless.  Even";
		mes "though it sounds random, there is";
		mes "a specific way of combining the";
		mes "words.";
		next;
		mes "[Karukatan]";
		mes "Also, our language has depends a";
		mes "a lot on emotion to convey its";
		mes "meaning.  Outsiders can't pick up";
		mes "the subtlety right away, even though";
		mes "we're the same race, ";
		next;
		mes "[Karukatan]";
		mes "It seems you have an interest";
		mes "in learning the language of the";
		mes "Utan tribe.  If that's the case,";
		mes "come visit me and I'll teach you.";
		next;
		mes "[Karukatan]";
		mes "Then, you won't sound so silly.";
		close;
	L3:
		mes "[Karukatan]";
		mes "Ah.  If you want to continue your";
		mes "trip, please keep this village in";
		mes "your memories.";
		close;
LStart3:
	if ((countitem(7151)>=10) && (countitem(7111)>=5) && (countitem(1024)>=1) && (countitem(916)>=1)) goto Lchkok;//Items: Oiled Paper, String Paper, Squid Ink, Feather of Birds,
	mes "[Karukatan]";
	mes "It seems you've not yet prepared the";
	mes "proper items.  Did you forget what";
	mes "you need to gather before I can";
	mes "teach you the language?  You need to gather";
	next;
	mes "[Karukatan]";
	mes "^3377FF10x Oil Paper^000000,";
	mes "^3377FF5x Slick Paper^000000,";
	mes "^3377FF1x Squid Ink^000000, and";
	mes "^3377FF1x Feather of Bird^000000.";
	mes "Once you have gathered those, I can teach";
	mes "you Utan tribal language.";
	close;
Lchkok:
	delitem 7151,10;//Items: Oiled Paper,
	delitem 7111,5;//Items: String Paper,
	delitem 1024,1;//Items: Squid Ink,
	delitem 916,1;//Items: Feather of Birds,
	mes "[Karukatan]";
	mes "Good.  You've prepared all the";
	mes "necessary materials.  Now, I can";
	mes "begin to teach you the Utan tribal";
	mes "language.";
	next;
	mes "[Karukatan]";
	mes "............";
	next;
	mes "[Karukatan]";
	mes "............";
	mes "............";
	next;
	mes "[Karukatan]";
	mes "............";
	mes "............";
	mes "............";
	next;
	mes "[Karukatan]";
	mes "Do you feel more enlightened now?";
	mes "Try talking to others in the Utan";
	mes "tribe.  They'll warm up to your quickly";
	mes "now that you speak the language.";
	set event_umbala,3;
	next;
	mes "[Karukatan]";
	mes "If you need something while you're";
	mes "here, don't hesitate to visit me.";
	close;
LStart4:
//fin
	mes "[Karukatan]";
	mes "Do you need something?";
	mes "If you need someone to guide you around";
	mes "the village, you should ask someone else.";
	mes "I can't leave here right now.";
	next;
	mes "[Karukatan]";
	mes "The best way to get a feel for";
	mes "the village is to just walk";
	mes "around and see the sights.  By doing";
	mes "that, you can form a personal";
	mes "bond with the village.";
	close;
LStart5:
	mes "[Karukatan]";
	mes "Oh, it seems you're enjoying yourself here.";
	mes "You need something else?";
	next;
	mes "[Karukatan]";
	mes "It seems you want to ask the";
	mes "shaman Putsuchiratan something";
	mes "and you're seeking my permission";
	mes "to do so.";
	next;
	mes "[Karukatan]";
	mes "Well...There's a mask from";
	mes "abroad that Putsuchiratan and I";
	mes "have both always desired.  Putsuchiratan";
	mes "heard about it some time ago.";
	next;
	mes "[Karukatan]";
	mes "If I could have the ^3377FFSmile Mask^000000,";
	mes "it would be a great honour, but";
	mes "I promised I would give it to";
	mes "to Putsuchiratan as a gift if I ever";
	mes "came across one.  If you can find a Smile Mask for me,";
	mes "I'll give you permission to speak with her.";
	set event_umbala,5;
	close;
LStart6:
	if(countitem(2278) > 0) goto Lsmileok;//Items: Mr. Smile,
	mes "[Karukatan]";
	mes "Did you find the ^3377FFSmile Mask^000000 yet?";
	mes "In order to let you see the shaman,";
	mes "you need to collect one.";
	mes "A gift will put Putsuchiritan in";
	mes "a good mood, also.";
	close;
Lsmileok:
	delitem 2278,1;//Items: Mr. Smile,
	mes "[Karukatan]";
	mes "Ahh!  It's a Smile Mask!";
	mes "I would really like to put this";
	mes "on just once, but I promised to";
	mes "give this to Putsuchiritan as";
	mes "a gift.";
	set event_umbala,6;
	next;
	mes "[Karukatan]";
	mes "I'll tell Putsuchiritan to allow you";
	mes "to meet with her.  I'm certain that you";
	mes "will find her talents to be a tremendous";
	mes "help on your journey.";
	close;
}

//=====================================================================
//                    Create Essence/Dismantle Stone
//=====================================================================
um_in.gat,44,71,2	script	Utan Tribe Shaman	782,{

	switch(event_umbala) {
	default:	
		mes "[?????]";
		mes "Umbaumbaumbaba Utan Umbaba";
		mes "Umbaumbaumbafumbabauma...";
		mes "Umbabaumbaumbabaumbabaumba";
		mes "Umbabaumbabaumbaumbabaumba";
		mes "Fumbaumba.";
		next;
		warp "umbala.gat",217,186;
		close;
	case 3:	
		mes "[Putsuchiritan]";
		mes "I will not see outsiders that are";
		mes "not from the Utan tribe.";
		mes "...There are many things about the";
		mes "nature of our society that you do";
		mes "not yet understand.";
		next;
		mes "[Putsuchiritan]";
		mes "Others of your type will do anything";
		mes "to set foot on land which is sacred";
		mes "to the Utan tribe.  We fear that";
		mes "revealing our secrets will expose";
		mes "use to danger.";
		next;
		mes "[Putsuchiritan]";
		mes "What's worse, I don't think that";
		mes "outsiders understand our desire";
		mes "to preserve our tribal culture.";
		next;
		mes "[Putsuchiritan]";
		mes "As my talents are foremost for";
		mes "the use of the Utan tribe, you";
		mes "must get permission from the elder";
		mes "if you want my assistance.";
		mes "Also, don't be stirring up trouble";
		mes "among the tribesmen.";
		set event_umbala,4;
		close;
	case 4:
	case 5:
		mes "[Putsuchiritan]";
		mes "Didn't you hear me?  You need";
		mes "to get the elder's permission before";
		mes "I can help you.";
		close;
	case 6:
		mes "[Putsuchiritan]";
		mes "You seem to have gotten permission from";
		mes "the elder.  Fine, I'll help you, though";
		mes "I don't really want to.";
		next;
		mes "[Putsuchiritan]";
		mes "I can call forth hidden talents";
		mes "buried deep within your psyche";
		mes "and tap the power hidden in";
		mes "elemental stones.";
		next;
		mes "[Putsuchiritan]";
		mes "I need to prepare my rituals.";
		mes "When you're ready to proceed, please";
		mes "talk to me again.";
		set event_umbala,7;
		close;
	case 7:
		mes "[Putsuchiritan]";
		mes "I don't know whether my talents";
		mes "will be useful to you, but I'll";
		mes "help you anyway.";
		next;
		mes "[Putsuchiritan]";
		mes "I can create elemental essence from natural objects,";
		mes "or, dismantle elemental stones into their component essences.";
		mes "Which would you like to do?";
	}
	
	next;

	set .@need,0;

	switch (select("Create Essence","Dismantle Elemental Stone","Nothing")) {
	case 1:
		mes "[Putsuchiritan]";
		mes "Which elemental essence would you";
		mes "like to create?  I can create earth, water,";
		mes "fire, and wind essence.";
		next;
		switch (select("Earth","Water","Fire","Wind")) {
			case 1:
				if(countitem(947)<15) {
					mes "[Putsuchiritan]";
					mes "Earth...You need that which is";
					mes "a friend of the earth.  Explore";
					mes "the verdant forest and find";
					mes "^3377FF15x Horn^000000 for the ritual.";
				}
				else {
					set .@need,947;
					set .@amount,15;
					set .@gain,993;
				}
				break;
			case 2:
				if(countitem(946)<20) {
					mes "[Putsuchiritan]";
					mes "Water...You need that which is";
					mes "saturated with water.  Explore";
					mes "the area around here and find";
					mes "^3377FF20x Snails Shell^000000 for the ritual.";
				}
				else {
					set .@need,946;
					set .@amount,20;
					set .@gain,991;
				}
				break;
			case 3:
				if(countitem(904)<20) {
					mes "[Putsuchiritan]";
					mes "Fire...You need that which";
					mes "can withstand the roiling heat";
					mes "of perpetual flame.  Explore the";
					mes "desert and find ^3377FF20x Scorpion Tail^000000";
					mes "for the ritual.";
				}
				else {
					set .@need,904;
					set .@amount,20;
					set .@gain,990;
				}
				break;
			case 4:
				if(countitem(1013)<25) {
					mes "[Putsuchiritan]";
					mes "Wind...You need that which";
					mes "has an easy camaraderie with";
					mes "the sky itself.";
					mes "You need ^3377FF25x Rainbow Shell^000000";
					mes "to complete the ritual.";
				}
				else {
					set .@need,1013;
					set .@amount,25;
					set .@gain,992;
				}
				break;
		}
		if(.@need == 0) {
			next;
			mes "[Putsuchiritan]";
			mes "Once you've prepared these";
			mes "objects from nature, please return here";
			mes "so we can complete the ritual.";
			close;
		}
		mes "[Putsuchiritan]";
		mes "Now, I will try to tap the";
		mes "source of power hidden deep within";
		mes "you.  Choose a number between 1 and 9.";
		mes "If you don't want to create essence";
		mes "after all, choose 0.";
		while(1) {
			next;
			input .@num;
			if(.@num<=0) {
				mes "[Putsuchiritan]";
				mes "If you don't wish my help at the moment,";
				mes "that's fine.  When you do, please come";
				mes "by again.";
				close;
			}
			if(.@num>9) {
				mes "[Putsuchiritan]";
				mes "You must choose a number between";
				mes "1 and 9.  It is very important that";
				mes "you choose the number most dear to";
				mes "your heart.";
				continue;
			}
			break;
		}
		mes "[Putsuchiritan]";
		switch(.@need) {
			case 947: mes "The horn begins to vibrate."; break;
			case 904: mes "The tail begins to vibrate."; break;
			default: mes "The shell begins to vibrate."; break;
		}
		mes "I will now chant a holy mantra.";
		mes "Imagine the number in your mind";
		mes "and imagine power radiating forth from";
		mes "your body.";
		next;
		mes "[Putsuchiritan]";
		mes "Amba Omba Sanba!  Unba Chi!";
		mes "Wanbatauma Eurukaba!  Unba!  Ba!";
		mes "Jyur!  Fumuba  Rurara!  Wamba!  Ra!";
		mes "Damtsuha Ombabaraka! Unba! Shi!";
		mes "Sukatamba Aburumba! Umba! Shi!";
		next;
		delitem .@need,.@amount;
		getitem .@gain,1;
		mes "[Putsuchiritan]";
		mes "There.  Your inner power has converted";
		mes "these objects from nature into";
		mes "elemental essence.  Put";
		mes "it to good use.";
		close;
	case 2:
		mes "[Putsuchiritan]";
		mes "What type of stone would you like to";
		mes "dismantle?  Please choose earth, water,";
		mes "fire, or wind.";
		next;
		switch (select("Earth","Water","Fire","Wind")) {
			case 1:
				if(countitem(997)==0) {
					mes "[Putsuchiritan]";
					mes "You need to have";
					mes "^3377FF1x Great Nature^000000";
					mes "to disassemble into its";
					mes "component essence.";
				}
				else {
					set .@need,997;
					set .@gain,993;
				}
				break;
			case 2:
				if(countitem(995)==0) {
					mes "[Putsuchiritan]";
					mes "You need to have";
					mes "^3377FF1x Mystic Frozen^000000";
					mes "to disassemble into its";
					mes "component essence.";
				}
				else {
					set .@need,995;
					set .@gain,991;
				}
				break;
			case 3:
				if(countitem(994)==0) {
					mes "[Putsuchiritan]";
					mes "You need to have";
					mes "^3377FF1x Flaming Heart^000000";
					mes "to disassemble into its";
					mes "component essence.";
				}
				else {
					set .@need,994;
					set .@gain,990;
				}
				break;
			case 4:
				if(countitem(996)==0) {
					mes "[Putsuchiritan]";
					mes "You need to have";
					mes "^3377FF1x Rough Wind^000000";
					mes "to disassemble into its";
					mes "component essence.";
				}
				else {
					set .@need,996;
					set .@gain,992;
				}
				break;
		}
		next;
		if(.@need == 0) {
			mes "[Putsuchiritan]";
			mes "Once you have the necessary stone,";
			mes "please come back and visit me";
			mes "so that the ritual can be completed.";
			close;
		}
		mes "[Putsuchiritan]";
		switch(.@need) {
			case 997: mes "I will dismantle your earth crystal"; 	break;
			case 995: mes "I will dismantle your frozen crystal"; 	break;
			case 994: mes "I will dismantle your heart of flame"; 		break;
			case 996: mes "I will dismantle your rough wind stone"; 	break;
		}
		mes "into its component essence.";
		mes "Choose a number between 1 and 9.";
		mes "If you don't want me to dismantle the";
		mes "stone, choose 0.";
		while(1) {
			next;
			input .@num;
			if(.@num<=0) {
				mes "[Putsuchiritan]";
				mes "If you don't wish my help at the moment,";
				mes "that's fine.  When you do, please come";
				mes "by again.";
				close;
			}
			if(.@num>9) {
				mes "[Putsuchiritan]";
				mes "You must choose a number between";
				mes "1 and 9.  It is very important that";
				mes "you choose the number most dear to";
				mes "your heart.";
				continue;
			}
			break;
		}
		set .@num,.@num+rand(5);
		if(.@num>10)
			set .@num,.@num-10;
		if(.@num<5)
			set .@num,.@num+5;
		mes "[Putsuchiritan]";
		mes "Please place the elemental stone";
		mes "inside this holy circle.  I will chant a";
		mes "a mantra.  I don't know if I can";
		mes "completely dismantle the stone,";
		mes "but here goes...";
		next;
		mes "[Putsuchiritan]";
		mes "Unba Unba Karama!  Unba!  Ta!";
		mes "Rukara!  Ukarere!  Un!  Unba!  Ka!";
		mes "Anburaka Taburaka Taburakan!  Unba Ra!";
		mes "Onbaruzu Zan Kata!  Unba Ka!";
		next;
		delitem .@need,1;
		getitem .@gain,.@num;
		mes "[Putsuchiritan]";
		mes "The elemental stone has been";
		mes "dismantled into its natural";
		mes "essence.  Please put it to";
		mes "good use.";
		close;
	case 3:
		mes "[Putsuchiritan]";
		mes "If you don't wish my help at the moment,";
		mes "that's fine.  When you do, please come";
		mes "by again.";
		close;
	}
}