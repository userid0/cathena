//===== eAthena Script ======================================= 
//= Quest NPCs related to Payon
//===== By: ================================================== 
//= kobra_k88
//===== Current Version: ===================================== 
//= 1.5a
//===== Compatible With: ===================================== 
//= eAthena 7.15 +
//===== Description: ========================================= 
//= Pretend Murder, Ear Muffs, Oxygen, Derivuchi Cap, Helm of Angel Quests.
//===== Additional Comments: ================================= 
//= Fully working
//= 1.1 Helm of Angel bug fixed [Lupus]
//= 1.2 Fixed skirt of virgin [Lupus]
//= 1.3 Fixed exploits [Lupus]
//= 1.4 Removed Duplicates [Silent]
//= 1.5 Updated Dialogs from "Pretend Murdered Quest" [Samuray22]
//= 1.5a replaced item "names" with item id. [Lupus]
//============================================================ 



//==========================================================================================//
//				Pretend Murdered quest
//==========================================================================================//
payon,154,170,4	script	Granny	78,{
	if(countitem(1049) > 3 ) {
		mes "[Granny in Payon]";
		mes "Hey... Is this the item for Skirt of Virgin tHat my children will wear for the coming festival...";
		mes "Could you give me some ^3355FF' Skirt of Virgin '^000000 for young girl... I need four of it... please ?";
		mes "I am begging you, please!";
		next;
		switch(select( "Give it to her.","No way!")) {
		case 1:
			delitem 1049,4;
			mes "[Granny in Payon]";
			mes "Uh-uh... Thank you very much! Now I can make Skirt of Virgin for my children.";
			mes "Hey... Oh! Yeah! I was in a stage play when I was young...";
			mes "This is the overcoat I kept when I left the troupe after I got married...";
			next;
			mes "[Granny in Payon]";
			mes "Take it if you like it... please!";
			mes "Good bye! Nice meeting you...";
			getitem 2293,1;
			close;
		case 2:
			mes "[Granny in Payon]";
			mes "Uh-uh... How can I get  ^3355FF'Skirt of Virgin '^000000 by myself... I am so weak and tired.";
			mes "It's such a cold world...";
			close;
		}
	}
		mes "[Granny in Payon]";
		mes "I gather Mushrooms on the Mountain of Payon Everyday for my living. I don't know how I will survive this month.";
		mes "... But festive is coming next month!";
		mes "My children requested for Skirt of Virgin for this festival.";
		mes "For Skirt of Virgin, I need ^3355FF' Skirt of Virgin '^000000 ...";
		next;
		mes "[Granny in Payon]";
		mes "Young man, I have a favor to ask... If you ever find ^3355FF' Skirt of Virgin '^000000... ";
		mes "Please come back to me. I will never forget your kindness...";
		close;
}


//========================================================================================//
//					Ear Muffs Quest
//========================================================================================//
payon_in01,18,10,4	script	Mystic Lady	75,{
	mes "[Mystic Lady]";
	mes "Hi there.";
	next;
	menu "Hello.",-,"Please make me a pair of Ear Muffs.",M_Muffs, "End",M_End;

		mes "[Mystic Lady]";
		mes "My family and I recently came to Payon.";
		mes "Where we came from the weather was very cold so we started a business hand making a special type of ^5555FF'Ear Muffs'^000000";
		next;
		mes "[Mystic Lady]";
		mes "I have tried to continue the family business here in Payon but it has proven to be difficult because of the warm whether.";
		next;
		mes "[Mystic Lady]";
		mes "Although the 'Ear Muffs' aren't very usefull here, if you plan on going somewhere that's cold our 'Ear Muffs' are a must have.";
		next;
		mes "[Mystic Lady]";
		mes "All I need are:";
		mes "^5555FF1 Cursed Ruby,";
		mes "1 Headset,";
		mes "200 Feathers,";
		mes "and a 5,000 zeny fee^000000 for my labor.";
		next;
		mes "[Mystic Lady]";
		mes "With those items I can make you a pair of 'Ear Muffs'.  Believe me, they are of the highest quality and look good on just about anybody!";
		close;
	
	M_Muffs:
		if(countitem(724)<1 || countitem(5001)<1 || countitem(949)<200 || zeny<5000) goto sL_NotEnuf;
		delitem 724,1;
		delitem 5001,1;
		delitem 949,200;
		set Zeny, Zeny-5000;
		mes "[Mystic Lady]";
		mes "Oh, I see that you have brought all of the required items.  Just a moment please...";
		next;
		mes "[Mystic Lady]";
		mes "... this goes here... that goes there...... some glue here... a couple stitches over there..........";
		next;
		getitem 2283,1;
		mes "[Mystic Lady]";
		mes "Ah, there you are! One pair of my familys' specialty Ear Muffs.  Enjoy!";
		close;

		sL_NotEnuf:
			mes "[Mystic Lady]";
			mes "I'm sorry but you don't have the required items for me to make you a pair of my familys' specialty Ear Muffs.";
			close;
	M_End:
		close;

}


//========================================================================================//
//					Oxygen Mask Quest
//========================================================================================//
payon_in02,25,71,4	script	Young Man#11	86,{
	mes "[Young Man]";
	if(countitem(701) >= 5) goto L_GotOra;
	mes "Blahhhhhh! I... I... I JUST CAN'T take it anymore!! You little stinky, filthy, BASTARDS!!!";
	emotion e_an;
	next;
	menu "Continue.",M_Cont, "End Conversation.",M_End;
	
	M_Cont:
		mes "[Young Man]";
		mes "~Sigh~ I've had a rough life.  You see, both my parents passed away when I was a kid, so I had to work to support myself at a young age.";
		next;
		mes "[Young Man]";
		mes "It sure was rough.  But after 10 years of hard work I was able to save up and finally buy myself a home!";
		next;
		mes "[Young Man]";
		mes "Unfortunately I didn't have that much money, so I couldn't buy the home I really wanted.";
		mes "Instead, I found this house..... It was big and cheap.... so I bought without really thinking my decision through.....";
		next;
		mes "[Young Man]";
		mes "Jeez was that a mistake...... The house turned out to be a HAVEN for THIEF BUGS!!!";
		emotion e_omg;
		next;
		mes "[Young Man]";
		mes "I tried EVERYTHING to get rid of them! I sprayed them, I smashed them, I lured them out with food.........";
		mes "Finally as a last resort.... I even tried to burn the house down!!";
		next;
		mes "[Young Man]";
		mes "Grrr! What will it take to get rid of these vile creatures???";
		emotion e_pif;
		next;
		mes "[Young Man]";
		mes "I've heard there is something called ^3355FF'Ora Ora'^000000, that people in other countries use to fight bugs.....";
		mes "But will it work for my situation?";
		next;
		mes "[Young Man]";
		mes "I'm willing to give anything a try at this point.  I don't have much money so I can only afford ^5555FF'5 Ora Oras'^000000.";
		mes "If you happen to come across some 'Ora Ora' please let me know.";
		close;

	M_End:
		mes "[Young Man]";
		mes "I'm just so tired of fighting this never ending battle against these bugs...... those dirty scumbags!!";
		emotion e_pif;
		close;

L_GotOra:
		mes "ArrrGgghhh!! No!... It can't be!..... I think I'm going mad... they're starting to look so cute... ArrrGgghhh!!... Nooooooo!!";
		emotion e_omg;
	M_Menu:
		next;
		menu "Continue",M_Cont, "Show Ora Ora",-, "Give Ora Ora",M_Give, "Cancel",M_End2;

		mes "[Young Man]";
		mes "Oooooohhhh..... is.. is this... is this... Ora Ora...??? If you give it to me I'll give you my prized treasure in return!";
		mes "What do you say?? Is it a deal?";
		goto M_Menu;

	M_Give:
		if(countitem(701) < 5) goto L_Cheater;
		delitem 701,5;
		mes "[Young Man]";
		mes "Muhahahahah!!! Finally, I have it.... Ora Ora! Stupid and distgusting Thief Bugs.... you'll pay for invading MY HOME!!!";
		emotion e_gg;
		next;
		mes "[Young Man]";
		mes "..... Eh em... sorry.  I was a little outa control there.... Ah, let me give you my valuable treasure......";	
		emotion e_swt;
		next;
		getitem 5004,1;
		mes "[Young Man]";
		mes "By wearing this over your mouth, the air you breathe will be filltered so that you can breathe clean air.";
		mes "I picked it up while I was a Sailor out at sea.  I really have no need for it.";
		next;
		mes "[Young Man]";
		mes "Hehehehe!! I can't wait to use this Ora Ora!!!";
		close;

	L_Cheater:
		mes "Oh... Where is it?!";
		next;

	M_End2:
		mes "[Young Man]";
		mes "What if I CAN'T get rid of these thief bugs???... I guess the only other thing I could do is fall in love with them...... heh... heh... heh....";
		emotion e_swt;
		close;

}


//========================================================================================//
//			Derivuchi Cap(Hat of Petite Diablo), Helm of Angel
//========================================================================================//
payon_in01,56,12,4	script	Young Man#12	89,{
	mes "[Young Man]";
	mes "What is it?............";
	next;
	menu "Can you make me a special item?",-, "Nothing.",M_End;

		mes "[Young Man]";
		mes "I see.... so you know about me huh?.... Ok, tell me what you want.";
		next;
		menu "-Helm of Angel",-, "-Deviruchi Hat",sM_Devir, "-I'll come back later.",M_End;

			mes "[Young Man]";
			mes "For the Helm of Angel I will need:";
			mes "- ^5555FF1 Helm (slotted)^000000.";
			mes "- ^5555FF1 Angel Wing^000000.";
			mes "- ^5555FF5 Fang of Garm^000000.";
			set @HAT, 1;
			next;
			goto sL_GetHat;
		sM_Devir:
			mes "[Young Man]";
			mes "For the Deviruchi Hat I will need:";
			mes "- ^5555FF600 Little Evil Horn^000000.";
			mes "- ^5555FF40 Talon of Griffon^000000.";
			set @HAT, 2;

			sL_GetHat:
				mes "[Young Man]";
				mes "Wait a moment!  Be carefull about what items you give me.";
				mes "I do not distinguish between equipment that has been forged or has cards attached to them.";
				mes "If you don't want to loose a precious card or piece of equipment, make sure you don't have it equiped.";
				next;
				mes "[Young Man]";
				mes "Do you have all of the items needed?";
				menu "-Give him the items.",-, "Nevermind.",M_End;

				if(@HAT == 2) goto ssL_Devir;

					if(countitem(2229)<1 || countitem(2254)<1 || countitem(7036)<5) goto ssL_NotEnuf;
					delitem 2229, 1;
					delitem 2254, 1;
					delitem 7036, 5;
					mes "[Young Man]";
					mes "Here is your Helm of Angel.";
					getitem 5025, 1;
					close;
				ssL_Devir:
					if(countitem(1038)<600 || countitem(7048)<40) goto ssL_NotEnuf;
					delitem 1038, 600;
					delitem 7048, 40;
					mes "[Young Man]";
					mes "Here is your Deviruchi Hat.";
					getitem 5038, 1;
					close;
				ssL_NotEnuf:
					mes "[Young Man]";
					mes "You don't have enough items for me to make what you want.";
					close;

	M_End:
		mes "[Young Man]";
		mes "Fine then.....";
		close;
}
