//===== eAthena Script ======================================= 
//= Alberta Town
//===== By: ================================================== 
//= kobra_k88
//===== Current Version: ===================================== 
//= 2.1
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//=
//===== Additional Comments: ================================= 
//= Fully working
//= 1.1 Negative input bug fixed [Lupus]
//= 1.2 Fixed typos in script [massdriller]
//= 1.3 Ran through spellcheck system [massdriller]
//= 1.4 Someone forget a  in an NPC header [Fredzilla]
//= 1.5 Fixed typo in script. [massdriller]
//= 1.6 Fixed some typos, optimized, added Gotanblue#2 to be able to return back to Alberta [Lupus]
//= 1.7 Fixed Phelix bugs [Lupus]
//= 1.8 Got rid of 2 useless variables, fixed Turtle Island Quest exploit [Lupus]
//= 1.9 Added two NPCs shown at RO Magazine [Playtester]
//= 2.0 Removed Duplicates [Silent]
//= 2.1 Fixed many typos, using information from Crono/Hollengrhen [Evera]
//============================================================ 

//NewsPaper(?)
alberta,148,53,1	script	Newspaper Girl	831,{end;}
//???
alberta,148,60,3	script	Man#03	833,{end;}

// Young Man -----------------------------------------------------------
alberta_in,20,183,6	script	Young Man	49,{
	mes "[Merchant]";
	mes "People say that ^0000ffGungnir^000000 is the legendary spear that never misses its target.  If that's true, then it's simply amazing.";
	close;
}

// Merchant -------------------------------------------------
alberta,97,51,7	script	Merchant#01	84,{
	mes "[Merchant]";
	mes "When you travel to many places like I do, you often hear a lot of rumors.  There is one rumor I heard that is really interesting.";
	next;
	mes "[Merchant]";
	mes "Apparently there are cards which seal the power of certain monsters within them.";
	mes "If someone is able to obtain one of those cards, he/she will obtain the power of the monster...";
	next;
	mes "[Merchant]";
	mes "Bah! I don't believe it though."; 
	close;
}

// Merchant ---------------------------------------------------------
alberta,53,39,8	script	Merchant#02	74,{
	mes "[Merchant]";
	mes "Oh, you look like a stranger.  Welcome to Alberta.";
	emotion e_gasp;
	next;
	mes "[Merchant]";
	mes "I was just brainstorming on some ideas I have for my business.";
	next;
	mes "[Merchant]";
	mes "You see, I heard that there is a store in Geffen that sells unique armor that is resistant to magic attacks.";
	mes "If I could somehow get them in bulk for a low price and sell them to the people in other towns......";
	next;
	mes "[Merchant]";
	mes "Cha-Ching! (you see dollar signs in her eyes)";
	emotion e_cash;
	close;
}

// Merchant -------------------------------------------------
alberta,58,80,8	script	Merchant#03	99,{
	mes "[Merchant]";
	if(rand(2)) goto R_1;
		mes "We merchants can open a roadside stand and do business.  With the ^0000ffDiscount skill^000000 we can buy goods from stores for low prices.";
		next;
		mes "[Merchant]";
		mes "We can also rent carts which allow us to load up with goods and make our business portable.";
		mes "This way, business is more convenient and safe.";
		close;
	R_1:
		mes "We merchants can negotiate with store NPCs to get more money for items we sell to them by using the skill ^ff0000Overcharge^000000.";
		next;
		mes "[Merchant]";
		mes "The most we can overcharge NPCs is by 24%, but it takes some hard work and training to get the skill!!";
		close;
}

// Phina -------------------------------------------------------
alberta,62,156,2	script	Phina	102,{
	set @TEMP,rand(3);
	mes "[Phina]";
	if(@TEMP == 1) goto R_1;
	if(@TEMP == 0) goto R_0;
		mes "This one time, I was walking in the forest and I saw a long, slender piece of green grass sticking out on the ground.";
		next;
		mes "[Phina]";
		mes "It was so cute that I wanted to touch it.  And when I did, you know what happened? The grass actually slapped my hand.";
		next;
		mes "[Phina]";
		mes "I was startled and so I backed off a bit.  I then realized that it was not grass but a very small creature.";
		emotion e_gasp;
		next;
		mes "[Phina]";
		mes "Even calm monsters can be very dangerous when they feel threatened.  So don't startle them by mistake.";
		close;
	R_1:
		mes "You know those dumb-looking bears that live in the forest connecting Alberta and Payon?";
		mes "You know... the ones that play around with the flies that make that buzzing noise.";
		next;
		mes "[Phina]";
		mes "Well, one time I threw a twig at one just for fun.  All of the sudden, IT RUSHED TOWARDS ME!";
		next;
		mes "[Phina]";
		mes "I was SOOOO scared!! I quickly jumped to the side to avoid it.  Then BAM!!! It hit a huge tree and crushed it into pieces.";
		emotion e_wah;
		next;
		mes "[Phina]";
		mes "I sure learned my lesson that day.  NEVER taunt any creature, 'cause if they get angry, you'll be in a world of hurt.";
		close;
	R_0:
   		mes "Did you know? Wolves are much more cooperative than they might seem.  If one of them is attacked, then others will come to help him.";
		next;
		mes "[Phina]";
   		mes "So be careful if you ever decide to fight one.";
		close;
}

// Grandma -----------------------------------------------------
alberta,93,174,2	script	Grandma	103,{
	mes "[Grandma]";
	mes "Some time ago a derelict ship drifted into the Alberta harbor.  Some of the town's young people went into the ship to find survivors.";
	next;
	mes "[Grandma]";
	mes "But after a few moments they all ran out terrified.  They said that they saw CORPSES walking around the ship!.";
	next;
	mes "[Grandma]";
	mes "The ship was also over run by never before seen sea monsters, which made it impossible for the townspeople to get around.";
	next;
	mes "[Grandma]";
	mes "We've never been able to do anything about that ominous-looking ship so we just left it there, hoping that it would sink or drift away.";
	next;
	mes "[Grandma]";
	mes "Then, out of nowhere, the Cool Event Corp. people came by and paid the city a huge amount zeny to buy the ship.";
	next;
	mes "[Grandma]";
	mes "They then created an event called the ^0000ddSunken Ship^000000.";
	next;
	mes "[Grandma]";
	mes "They invited young warriors from all over to enter the ghost ship and test their skills fighting the monsters in it.";
	next;
	mes "[Grandma]";
	mes "Now the ghost ship, that was once a problem for Alberta, has become quite a popular attraction.";
	next;
	mes "[Grandma]";
	mes "I have to say, though, I don't think it's worth risking your life for......";
	close;
}

// Drunken old man ----------------------------------------------------------------
alberta,131,139,4	script	Drunken old man	709,{
	mes "[Drunken old man]";
	mes "(~hiccup~)... Huh?... Wh-what are you staring at? Get lost!!";
	next;
	menu "Stay",-,"Leave him alone",M_1;
	
		mes "[Drunken old man]";
		mes "Hahahaha (~hiccup~)... So you got some nerve...";
		next;
		mes "[Drunken old man]";
		mes "Heh... I may look worthless now, but back in the day I was a handsome sailor on board the `Going Mary'.";
		next;
		menu "Is that a ship?",-,"Really? No kidding!",sM_1;
		
			mes "[Drunken old man]";
			mes "What? Ya never heard of it? Stupid! Everybody knows the notorious pirate ship `Going Mary'! (~hiccup~)";
			emotion e_what;
			next;
		sM_1:
			mes "[Drunken old man]";
			mes "Ah~ the good old days... only... if only we hadn't run into that STORM... (~hiccup~)";
			next;
			mes "[Drunken old man]";
			mes "AH~ Captain.  I miss our captain more than anything.... no foe could ever survive captain's sword.";
			mes "CAPTAIN~~~!!! (~HICCUP~) He'd swing his sword like THIS!... then... THEN...!!!";
			next;
			mes "[Drunken old man]";
			mes "The enemy and anything around him was surrounded by flames! Now that I think of it, the sword must've had some sort of mysterious power.";
			next;
			mes "[Drunken old man]";
			mes "(~sigh!~) (~sob, sob~)... God I miss everyone... Now I'm depressed.  Just go away and leave me be.....";
			emotion e_sob;
			close;
	M_1:
		mes "[Drunken old man]";
		mes "That's right! Go AWAY~";
		emotion e_pif;
		close;
}

// Soda Man ----------------------------------------------------
alberta,90,71,3	script	Soda Man	89,{
	mes "[Soda Man]";
	mes "Ummm.... delicious....";
	emotion e_ok;
	next;
	mes "[Soda Man]";
	mes "Wait! Don't bother me right now.  Hmm? What am I doing you ask?  Well isn't it obvious what I'm doing?";
	mes "Look at this! I've mixed sugar and soda together in this container.  Watch what happens when I heat it up.";
	next;
	mes "[Soda Man]";
	mes "The two ingredients will melt soon and intermix.  When the color of the mixture turns brown, we have to stop heating it.";
	mes "At that point it will have a marshmallow consistency.";
	next;
	mes "[Soda Man]";
	mes "You see what I am talking about now? Do you think it's valuable?........ What?..... No?.....";
	mes "Hmm... then what about the story of the old man who almost conquered ^5555FFTurtle Island^000000?";
	next;
	mes "[Soda Man]";
	mes "Go to the inn and you'll find an old drunkard there.  When you speak to him at first, it may seem like he's talking nonsense.";
	mes "But be patient and if you listen to his words carefully you may be able to pick up some useful information.";
	next;
	mes "[Soda Man]";
	mes "Oh, and there is a letter on the table inside of that old man's room.  It's a scary story about Turtle Island.";
	close;
}


//<=================================================== Marina (Docks/Port) ===================================================>\\
// Sailor Fisk ----------------------------------------------------------------
alberta,189,151,5	script	Sailor Fisk	100,{
	mes "[Sailor Fisk]";
	mes "Ahoy matey, where'd ya wanna go?";
	next;
	menu "Izlude Marina -> 500 Zeny.",-,"Quit",M_End;
	
		if(Zeny < 500) goto L_NoZeny;
		set Zeny, Zeny - 500;
		warp "izlude",176,182;
		close;

		L_NoZeny:
			mes "[Sailor Fisk]";
			mes "I'm sorry, but I told you I would need 500 Zeny, and it looks like you don't have it.";
			close;
	M_End:
		close;
}

// Phelix ------------------------------------------------------------------
alberta,190,173,4	script	Phelix	85,{

	mes "[Phelix]";
	mes "What the hell are you doing here?";
	next;
	mes "[Phelix]";
	mes "There is nothing you can get for free on this ship. If you want to get rewarded, do some work!!";
	next;
	mes "[Phelix]";
	mes "Hmm, however I'd be willing to trade you some items for your jellopies.";
	mes " - For ^0000ff10 Jellopies^000000 I'd be willing to give you ^ff00001 potion^000000.";
	mes " - For ^0000ff3 Jellopies^000000 I'll give you ^ff00001 sweet potato^000000.";
	next;
	mes "[Phelix]";
	mes "How does that sound?";
	next;
	menu "Sounds good",-,"Nah",M_End;
	
	M_Yes:
		mes "[Phelix]";
		mes "What do you want to exchange your Jellopies for?";
		next;
		menu "Red Potion please.",-,"Sweet Potato please.",M_1,"Cancel",M_End;

			set @item, 501;
			goto L_Get;

		M_1:
			set @item, 516;

			L_Get:
				mes "[Phelix]";
				mes "Please enter an amount.  Enter 0 to cancel.";
				next;
				set @input,0;
				input @input;
				if(@input < 1 || @input > 1000) goto M_Yes;
				if(checkweight(@item,@input)==0) goto L_OverW;

				if(@item == 501) set @amount, @input*10;
				if(@item == 516) set @amount, @input*3;
				if(countitem(909) < @amount) goto L_NotEnough;

				getitem @item,@input;
				delitem 909, @amount;
				close;
			
			L_NotEnough:
				mes "[Phelix]";
				mes "I'm sorry but you do not have enough Jellopies.";
				next;
				goto M_Yes;

			L_OverW:
				mes "[Phelix]";
				mes "I'm sorry but you can't carry so many things.";
				next;
				goto M_Yes;

	M_End:
		close;
}


//<=================================================== Sunken Ship ======================================================>\\
// Paul ----------------------------------------------------------------
alberta,195,151,3	script	Paul	86,{
	mes "[Paul]";
	mes "Good day.  Would you like be part of the Sunken Ship event, provided by Cool Event Corp.?";
	next;
	mes "[Paul]";
	mes "Oh! I better warn you, this event is only suitable for HIGH LEVEL warriors.";
	emotion e_gasp;
	next;
	mes "[Paul]";
	mes "So what do you say?  It only cost 200 zeny to participate and you'll get a ton of experience, guaranteed.";
	next;
	menu "Enter",-,"Quit",M_End;
	
		if(Zeny < 200) goto L_NoZeny;
		set Zeny, Zeny - 200;
		warp "alb2trea",62,69;
		close;
	M_End:
		mes "[Paul]";
		mes "Come back anytime.";
		close;

	L_NoZeny:
		mes "[Paul]";
		mes "I'm sorry, but I told you I would need 200 Zeny, and it looks like you don't have it.";
		mes "Please come back later when you have enough.";
		close;
}

// Sailor --------------------------------------------------
alb2trea,39,50,5	script	Sailor	100,{
	mes "[Sailor]";
	mes "Do you wanna return?";
	next;
	menu "Return to Alberta",-,"Quit",MEnd;

	warp "alberta",192,169;
MEnd:
	close;
}
