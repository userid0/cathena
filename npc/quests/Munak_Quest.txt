//===== eAthena Script ======================================= 
//= Wandering Misntrel Quest
//===== By: ================================================== 
//= Riotblade
//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= Trade Daenggie and Girl's Diary for "Her Heart" taming item.
//===== Comments: ============================================
//= Aegis conversion (cmd_baba.sc.) Needs optimizing.
//= Updated script dialog to iRO's
//===== Additional Comments: ================================= 
//= 1.0 First version, needs testing. [L0ne_W0lf]
//============================================================ 
comodo,112,182,0	script	Munak's Grandma	103,{
	if (countitem(1558) > 0) {
		mes "[Munak's grandma]";
		mes "Oh my...";
		mes "Have you seen my granddaughter,";
		if (Sex == 1) {
			mes "boy? My poor granddaughter";
		}
		else {
			mes "young lady? My poor granddaughter";
		}
		mes "has been missing...";
		next;
		mes "[Munak's Grandma]";
		mes "I can't remember exactly when it";
		mes "was, but when I lived in Payon, I";
		mes "had a cute granddaughter. She";
		mes "was really happy when I made her";
		mes "hair like ^000077Danggie^000000...";
		next;
		mes "[Munak's Grandma]";
		mes "She was alwaying working with the";
		mes "village chief... She was such a sweet";
		mes "girl, and always got along with";
		mes "chief's son...";
		next;
		mes "[Munak's Grandma]";
		mes "But one day our deity became angry";
		mes "and cursed the chief's son with a";
		mes "sickness! The village had to offer";
		mes "my granddaughter to him as a";
		mes "companion to lift the curse...";
		next;
		mes "[Munak's Grandma]";
		mes "The chief's son regained his";
		mes "health, but I lost my";
		mes "granddaughter! I can't look at him";
		mes "and not think of her, so I tried to";
		mes "leave my misery behind and came";
		mes "here to Comodo...";
		next;

		switch(select("Oh come on! Cheer up!:Um, is this diary...?")) {

		case 1:
			mes "[Munak's Grandma]";
			mes "It seems my granddaughter haunts my";
			mes "dreams every night. I believe I've";
			mes "been trying to cheer up for years";
			mes "now...";
			close;

		case 2:
			if (countitem(901) > 0) {
				mes "[Munak's Grandma]";
				mes "Oh god!";
				mes "It's my granddaughter's diary!";
				mes "Th-This is her writing! Oh my...!";
				next;
				mes "[Munak's Grandma]";
				if (Sex == 1) {
					mes "Young man... I'll read this to you if";
				}
				else {
					mes "Young lady... I'll read this to you if";
				}
				mes "you give it to me with a Danggie,";
				mes "please. I no longer have anything";
				mes "that belonged to her now...";
				next;

				switch(select("No way.:Ok, I'll.")) {

				case 1:
					mes "[Munak's Grandma]";
					mes "Oh...?";
					if (Sex == 1) {
						mes "Alright, young man.";
						mes "Thank you anyway.";
					}
					else {
						mes "Thank you anyway,";
						mes "young lady";
					}
					next;
					mes "[Munak's Grandma]";
					mes "It's alright...";
					mes "I can only hope that the deity is";
					mes "taking good care of her!";
					close;

				case 2:
					delitem 1558,1;
					delitem 901,1;
					mes "[Munak's Grandma]";
					if (Sex == 1) {
						mes "Oh!";
						mes "Thank you,";
						mes "young man~!";
					}
					else {
						mes "Goodness!";
						mes "Thank you,";
						mes "young lady...";
					}
					next;
					mes "[Munak's Grandma]";
					mes "Alright...";
					mes "I'll read this.";
					mes "Let me see...";
					next;
					mes "[Munak's Grandma]";
					mes "It seems that my granddaughter was";
					mes "treated by the chief like his own";
					mes "child! There are so many happy";
					mes "memories in this book!";
					next;
					getitem 659,1;
					mes "[Munak's Grandma]";
					mes "Oh, thank you. I now have a good";
					mes "keepsake of my granddaughter. Thank";
					mes "you so much! May God-Poing bless";
					mes "you!";
					close;

				}

			}

			else {
				mes "[Munak's Grandma]";
				mes "What? A diary? What's that diary...?";
				mes "I don't... I can't remember, oh my...";
				next;
				mes "[Munak's Grandma]";
				mes "I really wish I could make her hair";
				mes "into a ^000077Danggie^000000 again. I really";
				mes "wish... Oh, oh my granddaughter...";
				close;
			}


		}

	}

	else {
		mes "[Munak's Grandma]";
		mes "My own granddaughter...";
		mes "Why did she have to leave...?";
		close;
	}

}