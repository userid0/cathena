//===== eAthena Script ============
//= New hats from kRO Sakray on 2005-05-31
//===== By: =========================
//= Lorky, Lupus
//===== Current Version: ===================
//= 0.6
//===== Compatible With: =====================
//= eAthena 1.0
//===== Description: ============================================
//Hats lists:
//	Hat of the Investigator
//	Baby Suckle (Baby Rubber Nipple)
//	Mystic rose
//	Red Bonnet
//	Well-Baked Toast
//	Mask of Zealotus
//	Fashionable Warm Hat
//	n/a Soldier's Felt Hat (no info)
//===== Additional Comments: ==========================================
// 03.06.2005	:: Lorky ::	First release 0.1a
// 0.1b Fixed some missing things with temp IDs. [Lupus]
// 		Could someone correct wrong info, names, coords, etc? ^___-
// 0.1c Fixed hats shop. It shouldn't sell slotted hats.
// 0.2 Fixed Detective Hat items (by Ishizu-chan) [Lupus]
// 0.3 Added Zealotus mask quest with correct items [Lupus]
// 0.4 Fixed some sprites, npc names and coords [Lupus]
// 0.5 Removed temp shops, Black Clothes -> Piece of Black Quartz, thx to vicious_Pucca for note [Lupus]
// 0.5a Updated detective hat to ask for the correct items [Kayla]
// 0.6 Fixed possible exploits. Update yuor servers! [Lupus]
//=====================================================================

//Links (some info related to these headgears)
//http://translate.google.com/translate?hl=en&sl=ja&u=http://www11.big.or.jp/~top/2005/0625/head.htm
//http://eathena.deltaanime.net/board/index.php?showtopic=34214
//http://www.ragnainfo.net/forums/viewtopic.php?t=79677&start=0&postdays=0&postorder=asc&highlight=


//Quest Hats
//-------------------------------------------------------------------------------------------------
//Baby Suckle ID 5110
lighthalzen.gat,360,310,4	script	Boy	818,{  
	mes "[Boy]";
	mes "...MA...Ma..ma...";
	emotion 43;
	next;
	menu "Wow! Give me Baby Suckle!",-,"Nice Boy!",L_EXIT;

	mes "[Boy]";
	mes "Rrr..Rrr.. (^FF3300Royal jelly^000000)";			//526
	mes "Bu.. Ba...Ba-Bu-bi (^FF3300Baby Bottle^000000)";	//7270	(nursing bottle of baby )
	mes "Pa-Pa..Paxfi.. (^FF3300Pacifier^000000)";			//10004
	mes "Nu..Noos rie.. (^FF3300Nose Ring^000000)";			//941	(Nose Wheel)
	next;

	if(countitem(526)<1 || countitem(7270)<1 || countitem(10004)<1 || countitem(941)<1) goto L_EXIT;//Items: Royal_Jelly, Baby_Bottle, Pacifier, Nose_Ring,
	delitem 526,1;//Items: Royal_Jelly,
	delitem 7270,1;//Items: Baby_Bottle,
	delitem 10004,1;//Items: Pacifier,
	delitem 941,1;//Items: Nose_Ring,
	mes "[Boy]";
	mes "Th-anke...";
	emotion 43;
	getitem 5110,1;//Items: Baby_Rubber_Nipple,
	next;

L_EXIT:
	mes "[Boy]";
	mes "...B...Bye...";
	emotion 28;
	close;
	
}

//-------------------------------------------------------------------------------------------------
//Detective hat (Hat_of_the_Investigator) ID 5108
lighthalzen.gat,140,68,4	script	Morris	818,{
	mes "[Morris]";
	mes "Do you want a ^FF3300Hat of the Investigator^000000?"; 
	next;
	menu "Of course, I do",-,"No, thanks",L_EXIT;

	mes "[Morris]";
	mes "Ok then, I shall make one for you.";

	if(countitem(611)<10 || countitem(7301)<1887 || countitem(5120)<1 || Zeny<50000) goto L_NOITEM;//Items: Magnifier, Tassel, Wide_Bunker_Hat_,
	delitem 611,10;   //Items: Magnifier,
	delitem 7301,1887;  //Tassel//Items: Tassel,
	delitem 5120,1;   //Items: Wide_Bunker_Hat_,
	set Zeny,Zeny-50000;
	mes "[Morris]";
	mes "Very well. Now I can make it...";
	next;
	mes "[Morris]";
	mes "Enjoy.";
	getitem 5108,1;//Items: Detective_Hat,
	close;

L_NOITEM:
	mes "[Morris]";
	mes "Bring me:";
	mes "^FF3300 10 Magnifiers^000000,";
	mes "^FF3300 1887 Tassel^000000,";
	mes "^FF3300 1 Wide Bunker Hat [1]^000000,";
	mes "and ^FF3300 50,000 zeny.^000000";
	close;

L_EXIT:
	mes "[Morris]";
	mes "Bye.";
	close;
}

//-------------------------------------------------------------------------------------------------
//Mystic rose ID 5117
lhz_in02.gat,103,31,4	script	Margaret Mary	818,{
	mes "[Margaret Mary]";
	mes "Wheee! Don't you just love making youself look pretty!?";
	mes "I love glamourising myself with all the best accessories!";
	next;

	mes "[Margaret Mary]";
	mes "Have you ever heard of a ^FF3300Mystic Rose^000000?";
	mes "I can make you one, and you could be fashionable and mysticly, too!";
	next;
	menu "Make me a ^670000Mystic rose^000000.",-,"No, I dont want one.",L_EXIT;
	
	if(countitem(731) < 10 || countitem(748) < 3 || countitem(982) < 1 || Zeny<50000) goto L_NOITEM;//Items: 2_Carat_Diamond, Witherless_Rose, White_Dyestuff,
	delitem 731,10;//Items: 2_Carat_Diamond,
	delitem 748,3;//Items: Witherless_Rose,
	delitem 982,1;//Items: White_Dyestuff,
	set Zeny,Zeny-50000;
	mes "[Margaret Mary]";
	mes "Wow, thank you! Hand me your items, please.";
	next;
	mes "[Margaret Mary]";
	mes "Enjoy being pretty and mystic!";
	getitem 5117,1;//Items: Mystic_Rose,
	close;
L_NOITEM:
	mes "[Margaret Mary]";
	mes "Well, for me to make you my Mystic rose...";
	mes "I need these materials:";
	mes "^FF3300 10 2-Carat Diamonds^000000,";
	mes "^FF3300 3 Witherless Roses^000000,";
	mes "^FF3300 1 White Dyestuff^000000,";
	mes "and ^FF3300 50,000 zeny.^000000";
	close;
L_EXIT:
	mes "[Margaret Mary]";
	mes "Aww... well, OK then!";
	mes "Be pretty some other time";
	close;
}

//-------------------------------------------------------------------------------------------------
//Red hood (Red Bonnet) ID 5109
lighthalzen.gat,180,88,4	script	Rich Girl	818,{
	mes "[Rich Girl]";
	mes "Hello there! Please, answer my questions.";
	next;
	mes "[Rich Girl]";
	mes "Do you have a grandma?";
	emotion 20;
	next;
	mes "[Rich Girl]";
	mes "Really? I'm sorry. Do you like wolves?";
	next;
	mes "[Rich Girl]";
	mes "Ha! I knew it! And... my last question. Would you like to put on something reddish?";
	emotion 1;
	next;
	mes "[Rich Girl]";
	mes "Just kidding, Have you ever read 'Red-Hood'? I can make to you the very same ^FF3300Red Bonnet^000000.";
	next;
	menu "Please, make it!",-,"No, thanks",L_EXIT;
	if(countitem(10015) < 1 || countitem(975) < 1 || countitem(10007) < 1 || countitem(5032)<1 || Zeny<50000) goto L_NOITEM;//Items: Green_Lace, Scarlet_Dyestuffs, Silk_Ribbon, Sunday_Hat,
	delitem 10015,1;//Items: Green_Lace,
	delitem 975,1;//Items: Scarlet_Dyestuffs,
	delitem 10007,1;//Items: Silk_Ribbon,
	delitem 5032,1;//Items: Sunday_Hat,
	set Zeny,Zeny-50000;
	mes "[Rich Girl]";
	mes "Ok then, I shall make one for you.";
	next;
	mes "[Rich Girl]";
	mes "Now you can drink tea with your grannie and have fun with wolves.";
	getitem 5109,1;//Items: Red_Bonnet,
	close;

L_NOITEM:
	mes "[Rich Girl]";
	mes "Give me:";
	mes "^FF3300 Green Lace^000000,";
	mes "^FF3300 Scarlet Dyestuff ^000000,";
	mes "^FF3300 Silk Ribbon^000000,";
	mes "^FF3300 Sunday Hat ^000000,";
	mes "and ^FF3300 50,000 zeny.^000000";
	close;

L_EXIT:
	mes "[Rich Girl]";
	mes "Well done. Bye.";
	close;
}

//-------------------------------------------------------------------------------------------------
//Well-Baked Toast ID 5107
yuno.gat,224,116,4	script	Kacis	818,{
	mes "[Kacis]";
	mes "Do you want a ^FF3300 Well-Baked Toast ^000000?";
	next;
	menu "Please, make one for me",-,"No, thanks. I'm so full",L_EXIT;

	if(countitem(519) < 50 || countitem(548) < 50 || countitem(539) < 50 || countitem(7031)<50) goto L_NOITEM;//Items: Milk, Cheese, Piece_of_Cake, Old_Frying_Pan,
	delitem 519,50;//Items: Milk,
	delitem 548,50;//Items: Cheese,
	delitem 539,50;//Items: Piece_of_Cake,
	delitem 7031,50;//Items: Old_Frying_Pan,
	mes "[Kacis]";
	mes "Good, thank you! Let me just take your items.";
	next;
	mes "[Kacis]";	
	mes "Well done. Be careful, it's hot.";
	getitem 5107,1;//Items: Well_Done_Toast,
	close;

L_NOITEM:
	mes "[Kacis]";
	mes "Well, for me to make you ^FF3300 Well-Baked Toast ^000000...";
	mes "I require these materials:";
	mes "^FF3300 50 Milk, ^000000";
	mes "^FF3300 50 Cheese, ^000000";
	mes "^FF3300 50 Piece of Cake, ^000000";
	mes "^FF3300 50 Old Frying Pan. ^000000";
	close;
L_EXIT:
	mes "[Kacis]";
	mes "Ok. I more to get tasty toasts.";
	close;
}

//-------------------------------------------------------------------------------------------------
// Mask_of_Zealotus id 5121
gl_prison1.gat,138,141,4	script	Zealotus	1200,{
	mes "[Gill Tass]";
	mes "Do you want an unique ^FF3300Mask of Zealotus^000000 ?";
	next;
	menu "Yes, I do!",-,"No, thanks",L_EXIT;

	if(countitem(7315) < 369//Items: Black Quartz Piece
		|| countitem(660) < 1//Items: Forbidden_Red_Candle,
		|| countitem(7263) < 1//Items: Cat's_Eye,
		|| countitem(7099) < 30) goto L_NOITEM;//Items: Worn-out_Magic_Scroll,

	delitem 7315,369;//Items: Black Quartz Piece,
	delitem 660,1;//Items: Forbidden_Red_Candle,
	delitem 7263,1;//Items: Cat's_Eye,
	delitem 7099,30;//Items: Worn-out_Magic_Scroll,
	getnameditem 5121,strcharinfo(0);
	logmes "Quest: Got Zealotus Mask";

	mes "[Gill Tass]";
	mes "Just let me sign your unique Zealotus Mask.";
	mes "So all masks should be signed...";
	close;

L_NOITEM:
	mes "[Gill Tass]";
	mes "All right. Bring me these items:";
	mes "^FF3300 369 Pieces of Black Quartz^000000,";
	mes "^FF3300 1 Forbidden Red Candle^000000,";
	mes "^FF3300 1 Cat's Eye^000000";
	mes "and ^FF3300 30 Worn-out Magic scrolls.^000000";
	close;

L_EXIT:
	mes "[Gill Tass]";
	mes "As you wish...";
	close;
}
//-------------------------------------------------------------------------------------------------
//Fashionable Warm Hat ID 5115
einbech.gat,70,200,7	script	Mater	818,{
	mes "[Mater]";
	mes "Aren't you cold?";
	mes "I can make you a ^FF3300Winter Hat^000000";
	next;
	menu "Please, make one for me",-,"No, thanks",L_EXIT;

	if(countitem(7267) < 999 || countitem(983) < 1 || countitem(749) < 1 || Zeny<50000) goto L_NOITEM;//Items: Tigerskin_Underwear, Black_Dyestuff, Frozen_Rose,
	delitem 7267,999;//Items: Tigerskin_Underwear,
	delitem 983,1;//Items: Black_Dyestuff,
	delitem 749,1;//Items: Frozen_Rose,
	set Zeny,Zeny-50000;
	mes "[Mater]";
	mes "Exellent! Now give me the items.";
	next;
	mes "[Mater]";
	mes "Here you are.";
	getitem 5115,1;//Items: Fashionable_Fur_Hat,
	close;
L_NOITEM:
	mes "[Mater]";
	mes "Please, correct these materials:";	
	mes "^FF3300 999 Tigerskin Underwears^000000,";
	mes "^FF3300 Black Dyestuff^000000,";
	mes "^FF3300 Frozen Rose^000000,";
	mes "and ^FF3300 50,000 zeny.^000000";
	close;
L_EXIT:
	mes "[Mater]";
	mes "Azzz youu wishhhh... Brr-r.";
	close;
}
