//===== eAthena Script ======================================= 
//= Prontera Guides
//===== By: ================================================== 
//= kobra_k88
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= eAthena  0.5.2 +
//===== Description: ========================================= 
//=
//===== Additional Comments: ================================= 
//= Fully working
//= v1.1 Now using duplicate command.
//============================================================





// Center ---------------------------------------------------------
prontera.gat,154,187,4	script	Guide#1::Guide	105,{
	cutin "prt_soldier",2;
	mes "[Prontera Soldier]";
	mes "Welcome to Prontera, the capital city of Rune-Midgard Kingdom.";
	next;
	mes "[Prontera Soldier]";
	mes "We are here to help you find your way.  Please feel free to speak to us anytime you need help.";
M_Menu:
	next;
	menu "View Buildings",M_1, "Notice",M_2, "Cancel",M_End;

	M_1:
		mes "[Prontera Soldier]";
		mes "Do you want me to leave indications on the Mini-Map?";
		next;
		menu "No Thanks",sM_1a,"Yes Please",sM_1b;
		
		sM_1a:
			set @COMPASS_CHECK,0;
			goto L_Cont;

		sM_1b:
			set @COMPASS_CHECK,1;

		L_Cont:
		mes "[Prontera Soldier]";
		mes "Please choose a building to view.";
		next;
	sM_Menu:
		menu 	"Swordman Asosiation",M_1_1, "^9999F5Sanctuary^000000",M_1_2, "^ff0000Prontera Chivalry^000000",M_1_3, "Armory",M_1_4,
			"Item Shop",M_1_5, "Inn",M_1_6, "Trading Post",M_1_7, "Pub",M_1_8, "^FF8800Library^000000",M_1_9, "Job Agency",M_1_10,
			"^AABBCCCastle of Prontera^000000",M_1_11, "City Hall",M_1_12, "Forge Workshop",M_1_13,
			"Wipe all indications from mini-map.",M_1_14, "Cancel",M_End;

		M_1_1:
			mes "[Prontera Soldier]";
			mes "The 'Swordman Assosiation' has moved to Izlude, the Sattelite of Prontera.  That building is now empty";
			viewpoint 1,237,41,1,0x2222AA;
			goto sL_Wipe;
		M_1_2:
			mes "[Prontera Soldier]";
			mes "The main Church of the Rune-Midgard Kingdom, the `Santuary', is located in the Northeast part of town.";
			mes "This is where you go to pray or serve the church as an Acolyte.";
			viewpoint 1,236,316,2,0xAAAAF5;
			goto sL_Wipe;
		M_1_3:
			mes "[Prontera Soldier]";
			mes "The 'Prontera Chivalry' trains Knights and serves as Prontera's protectors.  It is located in the Northwest part of town.";
			viewpoint 1,46,345,3,0xFF0000;
			goto sL_Wipe;
		M_1_4:
			mes "[Prontera Soldier]";
			mes "The 'Armory' has an excellent selection of weapons and armor.";
			viewpoint 1,175,220,4,0xFF00FF;
			goto sL_Wipe;
		M_1_5:
			mes "[Prontera Soldier]";
			mes "You can find many usefull things at the 'Item Shop'.";
			viewpoint 1,134,221,5,0xFF00FF;
			goto sL_Wipe;
		M_1_6:
			mes "[Prontera Soldier]";
			mes "There are 2 'Inn's' located on the far Western and Eastern sides of town.";
			viewpoint 1,204,195,6,0x00DD55;
			viewpoint 1,107,212,7,0x00DD55;
			goto sL_Wipe;
		M_1_7:
			mes "[Prontera Soldier]";
			mes "The 'Trading Post' is located Southeast of the Fountain.";
			viewpoint 1,179,184,8,0x7901AD;
			goto sL_Wipe;
		M_1_8:
			mes "[Prontera Soldier]";
			mes "The 'Pub' is Prontera's hot spot.";
			viewpoint 1,208,154,9,0xAA0077;
			goto sL_Wipe;
		M_1_9:
			mes "[Prontera Soldier]";
			mes "The 'Library' houses a vast amount of information on the monsters of Rune-Midgard.  There are 2 Buildings that make up the 'Library'.";
			viewpoint 1,120,267,10,0xFF8800;
			viewpoint 1,192,267,11,0xFF8800;
			goto sL_Wipe;
		M_1_10:
			mes "[Prontera Soldier]";
			mes "The 'Job Agency' is located SouthWest of the Fountain.";
			viewpoint 1,133,183,12,0x4444FF;
			goto sL_Wipe;
		M_1_11:
			mes "[Prontera Soldier]";
			mes "The 'Castle Of Prontera' is located in the Northern most part of the city.";
			mes "On the other side of the Castle is North Prontera Field.";
			viewpoint 1,156,360,13,0xAABBCC;
			goto sL_Wipe;
		M_1_12:
			mes "[Prontera Soldier]";
			mes "'City Hall' is located in the SouthWest corner of town.";
			viewpoint 1,75,91,14,0xAAEE00;
			goto sL_Wipe;
	
		M_1_13:
			mes "[Prontera Soldier]";
			mes "The 'Forge Workshop' is where you can refine your weapons and armor.";
			viewpoint 1,180,183,15,0xFF4477;
			goto sL_Wipe;
		M_1_14:
			set @COMPASS_CHECK, 2;

		sL_Wipe:
			if(@COMPASS_CHECK != 2) next;	// avoids a double 'next' when 'wipe indications' is chosen
			if(@COMPASS_CHECK == 1) goto sM_Menu;
			viewpoint 2,237,41,1,0x2222AA;
			viewpoint 2,236,316,2,0xAAAAF5;
			viewpoint 2,46,345,3,0xFF0000;
			viewpoint 2,175,220,4,0xFF00FF;
			viewpoint 2,134,221,5,0xFF00FF;
			viewpoint 2,204,195,6,0x00DD55;
			viewpoint 2,107,212,7,0x00DD55;
			viewpoint 2,179,184,8,0x7901AD;
			viewpoint 2,208,154,9,0xAA0077;
			viewpoint 2,120,267,10,0xFF8800;
			viewpoint 2,192,267,11,0xFF8800;
			viewpoint 2,133,183,12,0x4444FF;
			viewpoint 2,156,360,13,0xAABBCC;
			viewpoint 2,75,91,14,0xAAEE00;
			viewpoint 2,180,183,15,0xFF4477;
			if (@COMPASS_CHECK == 0) goto sM_Menu;
			mes "[Prontera Soldier]";
			mes "All indications have been removed";
			cutin "prt_soldier",255;
			close;

	M_2:
		mes "[Prontera Soldier]";
		mes "We upgraded the Location Guide to the Newest Digital Style.  We hope you like this gorgeous new system.";
		next;
		mes "[Prontera Soldier]";
		mes "Don't forget to refer to the Mini-Map on the Upper-Right corner of your screen.";
		next;
		mes "[Prontera Soldier]";
		mes "If you can't see the Mini-Map, just hit ^0000ff'ctrl+tab'^000000 or Click the ^0000ff'map'^000000 button in the Basic Information Window";
		mes "Remeber to use the ^ff0000+,-^000000 buttons to adjust the map to your liking.";
		goto M_Menu;

	M_End:
		mes "[Prontera Soldier]";
		mes "Have a nice day.";
		cutin "prt_soldier",255;
		close;
}

// East ----------------------------------------------------
prontera.gat,282,208,2	duplicate(Guide)	Guide#2	105
//West --------------------------------------------------
prontera.gat,29,200,6	duplicate(Guide)	Guide#3	105
// South -----------------------------------------------------
prontera.gat,160,29,0	duplicate(Guide)	Guide#4	105
// North -----------------------------------------------
prontera.gat,151,330,4	duplicate(Guide)	Guide#5	105


// Guard -----------------------------------------------------
prontera.gat,160,330,4	script	Guard#1::Guard	105,{
	mes "Welcome to Prontera.";
	close;
}

prontera.gat,223,99,1	duplicate(Guard)	Guard#2	105
prontera.gat,229,104,1	duplicate(Guard)	Guard#3	105
prontera.gat,47,339,5	duplicate(Guard)	Guard#4	105
prontera.gat,52,344,5	duplicate(Guard)	Guard#5	105


//---------------------------------------------------------------------------------------- Prontera Maze ----------------------------------------------------------------------------------\\
prt_maze02.gat,100,69,4	script	Soldier	105,{
	mes "[Soldier]";
	mes "Yo yo, you'd be better not to go in there. It has rumored there is a Demon living in the forest.";
	mes "Of course it is just a rumor because no one see the people turning back from the palce.";
	mes "hmmm... it's up to you to go in or leave.";
	close;
}
prt_maze02.gat,110,69,4	script	Soldier	105,{
	mes "[Soldier]";
	mes "Sigh... the last guy entered this place haven't come back either. He didn't listen to me and went in to gather herbs or something like that...";
	mes "He was a really stupid, if I were him, I wouldn't give up my life just for those lame herbs or something.";
	close;
}
